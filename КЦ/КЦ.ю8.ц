взяти визначення ./КЦ;

зовнішня дія __КЦ__прочитати_файл(виділяч: адреса<біб::Виділяч>, шлях: адреса<КЦ::Шлях>, вихід: адреса<КЦ::Байти>, вихід_успіху: адреса<логічне>);

секція КЦ {
  дія змішати_назву(ключ: Назва, аргумент: невідома_адреса): позитивне {
    змінна змішане: позитивне = 5381;
    змінна п: позитивне = 0;

    поки п < ключ.значення.розмір {
      змішане = (змішане * 33) + позитивне(ключ.значення.дані[п]);
      п += 1;
    }

    вернути змішане;
  }

  дія перед_заміною_субʼєкта(ак: Назва, аз: Обʼєкт, бк: Назва, бз: Обʼєкт, аргумент: невідома_адреса) {
    змінна компілятор = аргумент як адреса<Компілятор>;

    якщо ак.вид == ВидНазвиКопія {
      звільнити_памʼять(компілятор, ак.значення.дані);
    }
  }

  дія перед_видаленням_субʼєкта(ключ: Назва, значення: Обʼєкт, аргумент: невідома_адреса) {
    змінна компілятор = аргумент як адреса<Компілятор>;

    якщо ключ.вид == ВидНазвиКопія {
      звільнити_памʼять(компілятор, ключ.значення.дані);
    }
  }

  зовнішня дія створити_середовище(компілятор: адреса<Компілятор>, зовнішнє: адреса<Середовище>): адреса<Середовище> {
    змінна середовище = виділити<Середовище>(компілятор);
    середовище.компілятор = компілятор;
    середовище.зовнішнє = зовнішнє;
    середовище.субʼєкти = біб::виділити_реєстр<Назва, Обʼєкт>(
      компілятор.виділяч,
      0,
      компілятор,
      змішати_назву,
      перевірити_назви,
      пусто,
      пусто
    );
    середовище.обʼєкт_секції = пусто;
    середовище.обʼєкт_дії = пусто;
    вернути середовище;
  }

  зовнішня дія створити_середовище_секції(компілятор: адреса<Компілятор>, зовнішнє: адреса<Середовище>, обʼєкт_секції: адреса<ОбʼєктСекції>): адреса<Середовище> {
    змінна середовище = створити_середовище(компілятор, зовнішнє);
    середовище.обʼєкт_секції = обʼєкт_секції;
    вернути середовище;
  }

  зовнішня дія створити_середовище_дії(компілятор: адреса<Компілятор>, зовнішнє: адреса<Середовище>, обʼєкт_дії: адреса<ОбʼєктДії>): адреса<Середовище> {
    змінна середовище = створити_середовище(компілятор, зовнішнє);
    середовище.обʼєкт_дії = обʼєкт_дії;
    вернути середовище;
  }

  зовнішня дія змінити_в_середовищі(середовище: адреса<Середовище>, назва: Назва, обʼєкт: Обʼєкт) {
    біб::змінити_в_реєстрі<Назва, Обʼєкт>(середовище.субʼєкти, назва, обʼєкт);
  }

  зовнішня дія знайти_в_середовищі(середовище: адреса<Середовище>, назва: Назва, вихід: адреса<Обʼєкт>): логічне {
    вернути біб::знайти_в_реєстрі<Назва, Обʼєкт>(середовище.субʼєкти, назва, вихід);
  }

  зовнішня дія знайти_в_середовищі_рекурсивно(середовище: адреса<Середовище>, назва: Назва, вихід: адреса<Обʼєкт>): логічне {
    змінна поточне_середовище = середовище;

    поки поточне_середовище != пусто {
      якщо біб::знайти_в_реєстрі<Назва, Обʼєкт>(поточне_середовище.субʼєкти, назва, вихід) {
        вернути так;
      }

      поточне_середовище = поточне_середовище.зовнішнє;
    }

    вернути ні;
  }

  дія створити_простий_тип(компілятор: адреса<Компілятор>, назва: Назва, пм_тип: адреса<ПМ::Тип>): адреса<ОбʼєктТипу> {
    змінна обʼєкт_типу = виділити_обʼєкт_типу(компілятор);
    обʼєкт_типу.вид = ВидОбʼєктаТипуПростий;
    обʼєкт_типу.пм_тип = пм_тип;
    обʼєкт_типу.дані = ДаніОбʼєктаТипуПростий {
      назва = назва
    };
    вернути обʼєкт_типу;
  }

  зовнішня дія створити_тип_адреси(компілятор: адреса<Компілятор>, тип_даних: адреса<ОбʼєктТипу>): адреса<ОбʼєктТипу> {
    змінна обʼєкт_типу = виділити_обʼєкт_типу(компілятор);
    обʼєкт_типу.вид = ВидОбʼєктаТипуАдреса;
    обʼєкт_типу.пм_тип = компілятор.тип_адреса.пм_тип;
    обʼєкт_типу.дані = ДаніОбʼєктаТипуАдреса {
      тип_даних = тип_даних
    };
    вернути обʼєкт_типу;
  }

  зовнішня дія створити_тип_памʼяті(компілятор: адреса<Компілятор>, тип_даних: адреса<ОбʼєктТипу>): адреса<ОбʼєктТипу> {
    змінна обʼєкт_типу = виділити_обʼєкт_типу(компілятор);
    обʼєкт_типу.вид = ВидОбʼєктаТипуПамʼять;
    обʼєкт_типу.пм_тип = компілятор.тип_памʼять.пм_тип;
    обʼєкт_типу.дані = ДаніОбʼєктаТипуПамʼять {
      тип_даних = тип_даних
    };
    вернути обʼєкт_типу;
  }

  зовнішня дія створити_тип_дії(компілятор: адреса<Компілятор>, параметри: Параметри, тип_результату: адреса<ОбʼєктТипу>): адреса<ОбʼєктТипу> {
    змінна обʼєкт_типу = виділити_обʼєкт_типу(компілятор);
    обʼєкт_типу.вид = ВидОбʼєктаТипуДія;
    обʼєкт_типу.пм_тип = компілятор.тип_адреса.пм_тип;

    змінна пм_параметри = ПМ::Параметри { 0, пусто, 0 };
    змінна п: позитивне = 0;
    поки п < параметри.розмір {
      ПМ::покласти_параметр(компілятор.пм_модуль, пм_параметри::адреса, ПМ::Параметр { параметри.дані[п].тип.пм_тип });
      п += 1;
    }

    змінна пм_тип_інструкції = ПМ::створити_тип_інструкції(
      компілятор.пм_модуль,
      пм_параметри,
      тип_результату.пм_тип
    );

    обʼєкт_типу.дані = ДаніОбʼєктаТипуДія {
      параметри = параметри,
      тип_результату = тип_результату,
      пм_тип_інструкції = пм_тип_інструкції
    };
    вернути обʼєкт_типу;
  }

  зовнішня дія створити_варіативний_тип(компілятор: адреса<Компілятор>, кількість_типів: позитивне, типи: памʼять<адреса<ОбʼєктТипу>>): адреса<ОбʼєктТипу> {
    змінна обʼєкт_типу = виділити_обʼєкт_типу(компілятор);
    обʼєкт_типу.вид = ВидОбʼєктаТипуВаріація;

    змінна найбільший_тип = типи[0];
    змінна розмір_найбільшого_типу: позитивне = ПМ::отримати_розмір_типу_для_виділення(компілятор.пм_модуль, типи[0].пм_тип);

    змінна п: позитивне = 1;

    поки п < кількість_типів {
      змінна тип = типи[п];
      змінна розмір = ПМ::отримати_розмір_типу_для_виділення(компілятор.пм_модуль, тип.пм_тип);

      якщо розмір > розмір_найбільшого_типу {
        найбільший_тип = тип;
        розмір_найбільшого_типу = розмір;
      }

      п += 1;
    }

    обʼєкт_типу.пм_тип = найбільший_тип.пм_тип;

    обʼєкт_типу.дані = ДаніОбʼєктаТипуВаріація {
      кількість_типів = кількість_типів,
      типи = типи,
      найбільший_тип = найбільший_тип
    };
    вернути обʼєкт_типу;
  }

  зовнішня дія створити_тип_т8(компілятор: адреса<Компілятор>): адреса<ОбʼєктТипу> {
    змінна параметри = Параметри { 2, виділити_памʼять<Параметр>(компілятор, 2), 2 };

    параметри.дані[0] = Параметр {
      назва = назва_зовнішня("розмір"),
      тип = компілятор.тип_н64,
      Місцезнаходження { пусто, 0 },
      Місцезнаходження { пусто, 0 }
    };
    параметри.дані[1] = Параметр {
      назва = назва_зовнішня("дані"),
      тип = створити_тип_памʼяті(компілятор, компілятор.тип_н8),
      Місцезнаходження { пусто, 0 },
      Місцезнаходження { пусто, 0 }
    };

    змінна обʼєкт_типу_структура = виділити_обʼєкт_типу(компілятор);
    обʼєкт_типу_структура.вид = ВидОбʼєктаТипуСтруктура;
    обʼєкт_типу_структура.дані = ДаніОбʼєктаТипуСтруктура {
      назва = назва_зовнішня("т8"),
      параметри = параметри,
      повна_назва = назва_зовнішня("т8")
    };

    змінна пм_параметри = ПМ::Параметри { 0, пусто, 0 };
    змінна п: позитивне = 0;
    поки п < параметри.розмір {
      ПМ::покласти_параметр(компілятор.пм_модуль, пм_параметри::адреса, ПМ::Параметр { параметри.дані[п].тип.пм_тип });
      п += 1;
    }

    обʼєкт_типу_структура.пм_тип = ПМ::створити_структуру(
      компілятор.пм_модуль,
      "т8",
      пм_параметри
    );

    вернути обʼєкт_типу_структура;
  }

  зовнішня дія створити_компілятор(виділяч: адреса<біб::Виділяч>): адреса<Компілятор> {
    змінна компілятор = біб::виділити<Компілятор>(виділяч);

    компілятор.виділяч = виділяч;

    компілятор.пм_модуль = ПМ::створити_модуль("а1", ПМ::ПРОЦЕСОР_Х86_64, ПМ::СИСТЕМА_ЛІНУКС_ГНУ);

    компілятор.глобальне_середовище = створити_середовище(компілятор, пусто);

    компілятор.перший_обʼєкт_дії = пусто;
    компілятор.останній_обʼєкт_дії = пусто;
    компілятор.перший_обʼєкт_типу = пусто;
    компілятор.останній_обʼєкт_типу = пусто;

    компілятор.тип_ніщо = створити_простий_тип(компілятор, назва_зовнішня("ніщо"), ПМ::отримати_тип_ніщо(компілятор.пм_модуль));
    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("ніщо"), Обʼєкт { ВидОбʼєктаТип, компілятор.тип_ніщо });

    компілятор.тип_адреса = виділити_обʼєкт_типу(компілятор);
    компілятор.тип_адреса.вид = ВидОбʼєктаТипуАдреса;
    компілятор.тип_адреса.пм_тип = ПМ::отримати_тип_адреса(компілятор.пм_модуль);
    компілятор.тип_адреса.дані = ДаніОбʼєктаТипуАдреса {
      тип_даних = компілятор.тип_ніщо
    };
    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("адреса"), Обʼєкт { ВидОбʼєктаТип, компілятор.тип_адреса });
    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("невідома_адреса"), Обʼєкт { ВидОбʼєктаТип, компілятор.тип_адреса });

    компілятор.тип_памʼять = виділити_обʼєкт_типу(компілятор);
    компілятор.тип_памʼять.вид = ВидОбʼєктаТипуПамʼять;
    компілятор.тип_памʼять.пм_тип = ПМ::отримати_тип_адреса(компілятор.пм_модуль);
    компілятор.тип_памʼять.дані = ДаніОбʼєктаТипуПамʼять {
      тип_даних = компілятор.тип_ніщо
    };
    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("памʼять"), Обʼєкт { ВидОбʼєктаТип, компілятор.тип_памʼять });
    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("невідома_памʼять"), Обʼєкт { ВидОбʼєктаТип, компілятор.тип_памʼять });

    компілятор.тип_логічне = створити_простий_тип(компілятор, назва_зовнішня("логічне"), ПМ::отримати_тип_б1(компілятор.пм_модуль));
    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("логічне"), Обʼєкт { ВидОбʼєктаТип, компілятор.тип_логічне });

    компілятор.тип_ц8 = створити_простий_тип(компілятор, назва_зовнішня("ц8"), ПМ::отримати_тип_б8(компілятор.пм_модуль));
    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("ц8"), Обʼєкт { ВидОбʼєктаТип, компілятор.тип_ц8 });

    компілятор.тип_ц16 = створити_простий_тип(компілятор, назва_зовнішня("ц16"), ПМ::отримати_тип_б16(компілятор.пм_модуль));
    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("ц16"), Обʼєкт { ВидОбʼєктаТип, компілятор.тип_ц16 });

    компілятор.тип_ц32 = створити_простий_тип(компілятор, назва_зовнішня("ц32"), ПМ::отримати_тип_б32(компілятор.пм_модуль));
    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("ц32"), Обʼєкт { ВидОбʼєктаТип, компілятор.тип_ц32 });

    компілятор.тип_ц64 = створити_простий_тип(компілятор, назва_зовнішня("ц64"), ПМ::отримати_тип_б64(компілятор.пм_модуль));
    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("ц64"), Обʼєкт { ВидОбʼєктаТип, компілятор.тип_ц64 });

    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("ціле"), Обʼєкт { ВидОбʼєктаТип, компілятор.тип_ц64 });

    компілятор.тип_н8 = створити_простий_тип(компілятор, назва_зовнішня("н8"), ПМ::отримати_тип_б8(компілятор.пм_модуль));
    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("н8"), Обʼєкт { ВидОбʼєктаТип, компілятор.тип_н8 });
    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("п8"), Обʼєкт { ВидОбʼєктаТип, компілятор.тип_н8 });

    компілятор.тип_н16 = створити_простий_тип(компілятор, назва_зовнішня("н16"), ПМ::отримати_тип_б16(компілятор.пм_модуль));
    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("н16"), Обʼєкт { ВидОбʼєктаТип, компілятор.тип_н16 });
    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("п16"), Обʼєкт { ВидОбʼєктаТип, компілятор.тип_н16 });

    компілятор.тип_н32 = створити_простий_тип(компілятор, назва_зовнішня("н32"), ПМ::отримати_тип_б32(компілятор.пм_модуль));
    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("н32"), Обʼєкт { ВидОбʼєктаТип, компілятор.тип_н32 });
    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("п32"), Обʼєкт { ВидОбʼєктаТип, компілятор.тип_н32 });

    компілятор.тип_н64 = створити_простий_тип(компілятор, назва_зовнішня("н64"), ПМ::отримати_тип_б64(компілятор.пм_модуль));
    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("н64"), Обʼєкт { ВидОбʼєктаТип, компілятор.тип_н64 });
    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("п64"), Обʼєкт { ВидОбʼєктаТип, компілятор.тип_н64 });

    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("натуральне"), Обʼєкт { ВидОбʼєктаТип, компілятор.тип_н64 });
    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("позитивне"), Обʼєкт { ВидОбʼєктаТип, компілятор.тип_н64 });

    компілятор.тип_р32 = створити_простий_тип(компілятор, назва_зовнішня("р32"), ПМ::отримати_тип_р32(компілятор.пм_модуль));
    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("р32"), Обʼєкт { ВидОбʼєктаТип, компілятор.тип_р32 });
    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("д32"), Обʼєкт { ВидОбʼєктаТип, компілятор.тип_р32 });

    компілятор.тип_р64 = створити_простий_тип(компілятор, назва_зовнішня("р64"), ПМ::отримати_тип_р64(компілятор.пм_модуль));
    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("р64"), Обʼєкт { ВидОбʼєктаТип, компілятор.тип_р64 });
    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("д64"), Обʼєкт { ВидОбʼєктаТип, компілятор.тип_р64 });

    компілятор.обʼєкт_значення_так = ОбʼєктЗначення { компілятор.тип_логічне, ПМ::створити_логічне(компілятор.пм_модуль, так) };
    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("так"), Обʼєкт { ВидОбʼєктаТип, компілятор.обʼєкт_значення_так });

    компілятор.обʼєкт_значення_ні = ОбʼєктЗначення { компілятор.тип_логічне, ПМ::створити_логічне(компілятор.пм_модуль, ні) };
    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("ні"), Обʼєкт { ВидОбʼєктаТип, компілятор.обʼєкт_значення_ні });

    компілятор.обʼєкт_значення_пусто = ОбʼєктЗначення { компілятор.тип_адреса, ПМ::отримати_значення_пусто(компілятор.пм_модуль) };
    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("пусто"), Обʼєкт { ВидОбʼєктаЗначення, компілятор.обʼєкт_значення_пусто });

    компілятор.тип_т8 = створити_тип_т8(компілятор);
    змінити_в_середовищі(компілятор.глобальне_середовище, назва_зовнішня("т8"), Обʼєкт { ВидОбʼєктаТип, компілятор.тип_т8 });

    вернути компілятор;
  }

  зовнішня дія знищити_компілятор(компілятор: адреса<Компілятор>) {
    // потім: знищити всі реєстри, накопичувачі, обʼєкти і тд
    біб::звільнити(компілятор.виділяч, компілятор);
  }

  дія система_КД_виділити_сиру_памʼять(система: адреса<КД::Система>, розмір: позитивне): памʼять<п8> {
    змінна компілятор = система.дані як адреса<Компілятор>;
    вернути виділити_памʼять<п8>(компілятор, розмір);
  }

  дія система_КД_перевиділити_сиру_памʼять(система: адреса<КД::Система>, значення: памʼять<п8>, новий_розмір: позитивне): памʼять<п8> {
    змінна компілятор = система.дані як адреса<Компілятор>;
    перевиділити_памʼять<п8>(компілятор, значення, новий_розмір);
  }

  дія система_КД_звільнити_сиру_памʼять(система: адреса<КД::Система>, значення: невідома_памʼять) {
    змінна компілятор = система.дані як адреса<Компілятор>;
    звільнити_памʼять(компілятор, значення);
  }

  зовнішня дія перекодувати_т8_в_ю8_з_місцезнаходженнями(компілятор: адреса<Компілятор>, значення: ю8, вихід: адреса<т8>, вихід_місцезнаходжень: адреса<КД::Місцезнаходження>): логічне {
    змінна система_КД = КД::Система {
      дані = компілятор,
      виділити_сиру_памʼять = система_КД_виділити_сиру_памʼять,
      перевиділити_сиру_памʼять = система_КД_перевиділити_сиру_памʼять,
      звільнити_сиру_памʼять = система_КД_звільнити_сиру_памʼять
    };

    вернути КД::перекодувати_ю8_в_т8_з_місцезнаходженнями(система_КД::адреса, значення, вихід, вихід_місцезнаходжень, ні);
  }

  зовнішня дія отримати_т8_з_ю8(компілятор: адреса<Компілятор>, значення: ю8): т8 {
    змінна результат = т8 { 0, пусто };

    змінна система_КД = КД::Система {
      дані = компілятор,
      виділити_сиру_памʼять = система_КД_виділити_сиру_памʼять,
      перевиділити_сиру_памʼять = система_КД_перевиділити_сиру_памʼять,
      звільнити_сиру_памʼять = система_КД_звільнити_сиру_памʼять
    };

    якщо КД::перекодувати_ю8_в_т8(система_КД::адреса, значення, результат::адреса, ні) == ні {
      // потім: обробити помилку
    }

    вернути результат;
  }

  зовнішня дія отримати_ю8_з_т8(компілятор: адреса<Компілятор>, значення: т8): ю8 {
    змінна результат = ю8 { 0, пусто };

    змінна система_КД = КД::Система {
      дані = компілятор,
      виділити_сиру_памʼять = система_КД_виділити_сиру_памʼять,
      перевиділити_сиру_памʼять = система_КД_перевиділити_сиру_памʼять,
      звільнити_сиру_памʼять = система_КД_звільнити_сиру_памʼять
    };

    якщо КД::перекодувати_т8_в_ю8(система_КД::адреса, значення, результат::адреса, ні) == ні {
      // потім: обробити помилку
    }

    вернути результат;
  }

  зовнішня дія прочитати_файл(компілятор: адреса<Компілятор>, шлях: Шлях, вихід: адреса<Байти>): логічне {
    змінна успіх = ні;
    __КЦ__прочитати_файл(компілятор.виділяч, шлях::адреса, вихід, успіх::адреса);
    вернути успіх;
  }

  зовнішня дія скомпілювати_текст_коду(компілятор: адреса<Компілятор>, текст_коду: адреса<ТекстКоду>, вихід_помилки: адреса<адреса<Помилка>>): логічне {
    змінна розбирач = КЦ::розбирач::створити(компілятор.виділяч);

    змінна успіх_розбору = ні;
    змінна розібраний_текст = РозібранийТекст {
      текст_коду = текст_коду,
      слова = КЦ::розбирач::Слова { 0, пусто, 0 },
      сполуки = КЦ::розбирач::Сполуки { 0, пусто, 0 }
    };
    змінна помилка_розбору_на_слова: КЦ::розбирач::ПомилкаРозборуНаСлова;
    змінна помилка_розбору_на_сполуки: КЦ::розбирач::ПомилкаРозборуНаСполуки;

    успіх_розбору = КЦ::розбирач::розібрати_на_слова(
      розбирач,
      т8 { текст_коду.значення.розмір, текст_коду.значення.дані },
      розібраний_текст.слова::адреса,
      помилка_розбору_на_слова::адреса
    );
    якщо успіх_розбору == ні {
      змінна позиція_в_тексті = помилка_розбору_на_слова.позиція.позиція_в_тексті;
      змінна рядок = помилка_розбору_на_слова.позиція.рядок;
      змінна стовпець = помилка_розбору_на_слова.позиція.стовпець;

      вихід_помилки::вміст = створити_помилку(
        компілятор,
        "Неочікуваний символ.",
        створити_місцезнаходження_помилки(компілятор, текст_коду, позиція_в_тексті, рядок, стовпець)
      );

      вернути ні;
    }

    успіх_розбору = КЦ::розбирач::розібрати_слова_на_сполуки(розбирач, розібраний_текст.слова, розібраний_текст.сполуки::адреса, помилка_розбору_на_сполуки::адреса);
    якщо успіх_розбору == ні {
      змінна позиція_в_тексті = розібраний_текст.слова.дані[помилка_розбору_на_сполуки.позиція_слова].позиція_початку.позиція_в_тексті;
      змінна рядок = розібраний_текст.слова.дані[помилка_розбору_на_сполуки.позиція_слова].позиція_початку.рядок;
      змінна стовпець = розібраний_текст.слова.дані[помилка_розбору_на_сполуки.позиція_слова].позиція_початку.стовпець;

      вихід_помилки::вміст = створити_помилку(
        компілятор,
        "Неочікуване слово.",
        створити_місцезнаходження_помилки(компілятор, текст_коду, позиція_в_тексті, рядок, стовпець)
      );

      вернути ні;
    }

    змінна результат_компіляції_тіла = скомпілювати_тіло(
      компілятор,
      Контекст {
        розібраний_текст = розібраний_текст::адреса,
        середовище = компілятор.глобальне_середовище,
        кк_дії = пусто
      },
      розібраний_текст.сполуки,
      розібраний_текст.сполуки.дані[0].місцезнаходження
    );

    якщо результат_компіляції_тіла.помилка != пусто {
      вихід_помилки::вміст = результат_компіляції_тіла.помилка;
      вернути ні;
    }

    // ...

    вернути так;
  }

  зовнішня дія взяти_файл_ю8(компілятор: адреса<Компілятор>, шлях: Шлях, вихід_помилки: адреса<адреса<Помилка>>): логічне {
    змінна код = Байти { 0, пусто };

    якщо прочитати_файл(компілятор, шлях, код::адреса) == ні {
      вихід_помилки::вміст = створити_помилку(компілятор, "Не вдалось прочитати файл.", пусто);

      вернути ні;
    }

    змінна текст_коду = створити_текст_коду_з_ю8(компілятор, шлях, ю8 { код.розмір, код.дані });

    вернути скомпілювати_текст_коду(компілятор, текст_коду, вихід_помилки);
  }

  зовнішня дія отримати_лл(компілятор: адреса<Компілятор>, вихід: адреса<ю8>, вихід_помилки: адреса<адреса<Помилка>>): логічне {
    ПМ::отримати_лл(компілятор.пм_модуль, вихід.розмір::адреса, вихід.дані::адреса);

    вернути так;
  }

  синонім СИМВОЛ_ПЛЮС = 12;
  синонім СИМВОЛ_МІНУС = 30;
  синонім СИМВОЛ_0 = 16;
  синонім СИМВОЛ_1 = 17;
  синонім СИМВОЛ_2 = 18;
  синонім СИМВОЛ_3 = 19;
  синонім СИМВОЛ_4 = 20;
  синонім СИМВОЛ_5 = 21;
  синонім СИМВОЛ_6 = 22;
  синонім СИМВОЛ_7 = 23;
  синонім СИМВОЛ_8 = 24;
  синонім СИМВОЛ_9 = 25;
  синонім СИМВОЛ_КРАПКА = 109;


  зовнішня дія спробувати_розібрати_ціле_число(компілятор: адреса<Компілятор>, значення: т8, вихід: адреса<ц64>, вихід_помилки: адреса<т8>, вихід_позиції_помилки: адреса<позитивне>): логічне {
    змінна вихідне: ціле = 0;
    змінна знак: ц8 = 1;
    змінна п: позитивне = 0;

    якщо п < значення.розмір {
      якщо значення.дані[п] == СИМВОЛ_МІНУС {
        знак = ц8(0) - 1;
        п += 1;
      } інакше якщо значення.дані[п] == СИМВОЛ_ПЛЮС {
        п += 1;
      }
    }

    поки п < значення.розмір {
      змінна с = значення.дані[п];

      якщо с == СИМВОЛ_0 {
      } інакше якщо с == СИМВОЛ_1 {
      } інакше якщо с == СИМВОЛ_2 {
      } інакше якщо с == СИМВОЛ_3 {
      } інакше якщо с == СИМВОЛ_4 {
      } інакше якщо с == СИМВОЛ_5 {
      } інакше якщо с == СИМВОЛ_6 {
      } інакше якщо с == СИМВОЛ_7 {
      } інакше якщо с == СИМВОЛ_8 {
      } інакше якщо с == СИМВОЛ_9 {
      } інакше {
        стрибнути успіх;
      }

      вихідне = вихідне * 10 + (ціле(с) - СИМВОЛ_0);

      п += 1;
    }

  успіх:
    вихід::вміст = вихідне * ціле(знак);
    вернути так;
  }

  зовнішня дія спробувати_розібрати_позитивне_число(компілятор: адреса<Компілятор>, значення: т8, вихід: адреса<п64>, вихід_помилки: адреса<т8>, вихід_позиції_помилки: адреса<позитивне>): логічне {
    змінна вихідне: позитивне = 0;
    змінна п: позитивне = 0;

    поки п < значення.розмір {
      змінна с = значення.дані[п];

      якщо с == СИМВОЛ_0 {
      } інакше якщо с == СИМВОЛ_1 {
      } інакше якщо с == СИМВОЛ_2 {
      } інакше якщо с == СИМВОЛ_3 {
      } інакше якщо с == СИМВОЛ_4 {
      } інакше якщо с == СИМВОЛ_5 {
      } інакше якщо с == СИМВОЛ_6 {
      } інакше якщо с == СИМВОЛ_7 {
      } інакше якщо с == СИМВОЛ_8 {
      } інакше якщо с == СИМВОЛ_9 {
      } інакше {
        стрибнути успіх;
      }

      вихідне = вихідне * 10 + (позитивне(с) - СИМВОЛ_0);

      п += 1;
    }

  успіх:
    вихід::вміст = вихідне;
    вернути так;
  }

  зовнішня дія спробувати_розібрати_дійсне_число(компілятор: адреса<Компілятор>, значення: т8, вихід: адреса<д64>, вихід_помилки: адреса<т8>, вихід_позиції_помилки: адреса<позитивне>): логічне {
    змінна вихідне: д64 = 0.0;
    змінна фрак: д64 = 0.0;
    змінна дів: д64 = 10.0;
    змінна п: позитивне = 0;
    змінна знак: ц8 = 1;
    змінна була_крапка = ні;

    якщо п < значення.розмір {
      змінна с = значення.дані[п];

      якщо с == СИМВОЛ_МІНУС {
        знак = ц8(0)-1;
        п += 1;
      } інакше {
        якщо с == СИМВОЛ_ПЛЮС {
          п += 1;
        }
      }
    }

    поки п < значення.розмір {
      змінна с = значення.дані[п];
      змінна с_крапка = с == СИМВОЛ_КРАПКА;
      змінна с_цбр_0 = с >= СИМВОЛ_0;
      змінна с_цмр_9 = с <= СИМВОЛ_9;
      змінна с_цифра = с_цбр_0 * с_цмр_9;

      змінна вже_була_крапка = була_крапка;

      якщо с_крапка {
        якщо вже_була_крапка {
          стрибнути успіх;
        }

        була_крапка = так;

        п += 1;
      } інакше {
        якщо с_цифра == ні {
          стрибнути успіх;
        }

        якщо вже_була_крапка {
          фрак = фрак + (д64(с) - СИМВОЛ_0) / дів;
          дів = дів * 10.0;
        } інакше {
          вихідне = вихідне * 10.0 + (д64(с) - СИМВОЛ_0);
        }

        п += 1;
      }
    }

  успіх:
    вихід::вміст = д64(знак) * (вихідне + фрак);
    вернути так;
  }

  зовнішня дія перетворити_позитивне_на_т8(компілятор: адреса<Компілятор>, значення: позитивне, вихід: адреса<т8>): логічне {
    вернути ні;
  }

  зовнішня дія отримати_повну_назву_в_контексті(компілятор: адреса<Компілятор>, контекст: Контекст, назва: т8): т8 {
    змінна накопичувач_т8 = зробити_накопичувач_т8(компілятор);

    покласти_в_накопичувач_т8(компілятор, накопичувач_т8::адреса, назва);

    змінна поточне_середовище = контекст.середовище;

    поки поточне_середовище != пусто {
      якщо поточне_середовище.обʼєкт_секції != пусто {
        якщо поточне_середовище.обʼєкт_секції.назва.значення.розмір > 0 {
          покласти_спереду_накопичувача_т8(компілятор, накопичувач_т8::адреса, "::");
          покласти_спереду_накопичувача_т8(компілятор, накопичувач_т8::адреса, поточне_середовище.обʼєкт_секції.назва.значення);
        }
      } інакше якщо поточне_середовище.обʼєкт_дії != пусто {
        якщо поточне_середовище.обʼєкт_дії.назва.значення.розмір > 0 {
          покласти_спереду_накопичувача_т8(компілятор, накопичувач_т8::адреса, "::");
          покласти_спереду_накопичувача_т8(компілятор, накопичувач_т8::адреса, поточне_середовище.обʼєкт_дії.назва.значення);
        }
      }

      поточне_середовище = поточне_середовище.зовнішнє;
    }

    вернути накопичувач_т8.значення;
  }
}
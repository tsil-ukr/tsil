взяти визначення ./КЦ;

секція КЦ {
  дія перед_заміною_в_реєстрі(ак: Шлях, аз: адреса<РозібранийТекст>, бк: адреса<Шлях>, бз: адреса<адреса<РозібранийТекст>>, аргумент: невідома_адреса) {
    змінна компілятор = аргумент як адреса<Компілятор>;

    знищити_шлях(компілятор, ак);
  }

  дія перед_видаленням_з_реєстру(ключ: Шлях, значення: адреса<РозібранийТекст>, аргумент: невідома_адреса) {
    змінна компілятор = аргумент як адреса<Компілятор>;

    знищити_шлях(компілятор, ключ);
  }

  зовнішня дія створити_взяті_файли(компілятор: адреса<Компілятор>): адреса<ВзятіФайли> {
    змінна взяті_файли = виділити<ВзятіФайли>(компілятор);

    взяті_файли.компілятор = компілятор;
    взяті_файли.реєстр = біб::виділити_реєстр<Шлях, адреса<РозібранийТекст>>(
      компілятор.виділяч,
      0,
      компілятор,
      змішати_шлях_для_реєстру,
      перевірити_шляхи_в_реєстрі,
      перед_заміною_в_реєстрі,
      перед_видаленням_з_реєстру
    );

    вернути взяті_файли;
  }

  зовнішня дія знищити_взяті_файли(взяті_файли: адреса<ВзятіФайли>) {
    змінна компілятор = взяті_файли.компілятор;

    біб::звільнити_реєстр<Шлях, адреса<РозібранийТекст>>(взяті_файли.реєстр);

    звільнити(компілятор, взяті_файли);
  }

  зовнішня дія покласти_взятий_файл(взяті_файли: адреса<ВзятіФайли>, шлях: Шлях, розібраний_текст: адреса<РозібранийТекст>) {
    змінна компілятор = взяті_файли.компілятор;

    шлях = клонувати_шлях(компілятор, шлях);

    біб::змінити_в_реєстрі<Шлях, адреса<РозібранийТекст>>(
      взяті_файли.реєстр,
      шлях,
      розібраний_текст
    );
  }

  зовнішня дія знайти_взятий_файл(взяті_файли: адреса<ВзятіФайли>, шлях: Шлях, вихід: адреса<адреса<РозібранийТекст>>): логічне {
    вернути біб::знайти_в_реєстрі<Шлях, адреса<РозібранийТекст>>(
      взяті_файли.реєстр,
      шлях,
      вихід
    );
  }
}
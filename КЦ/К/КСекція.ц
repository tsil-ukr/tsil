взяти визначення К;

секція цк {
  місцева дія скомпілювати_секцію(К: комірка<Компілятор>, блок: комірка<Блок>, інформація_виконання_шаблону: комірка<ІнформаціяВиконанняШаблону>, середовище: комірка<Середовище>, місцезнаходження: комірка<Місцезнаходження>, дані: комірка<АСДДаніСекція>): РезультатЗначення {
    ціль назва = дані.ідентифікатор.значення;

    ціль середовище_створення_секції = створити_середовище(середовище);

    ціль середовище_секції = створити_середовище(середовище_створення_секції);

    ціль секція_ = виділити<Секція>();
    секція_.назва = назва;
    секція_.середовище = середовище_секції;

    середовище_секції.секція_ = секція_;

    ціль обʼєкт_компіляції = ОбʼєктКомпіляції { ВидОКСекція, секція_ };
    ціль значення = Значення { ВидЗначенняОК, к = обʼєкт_компіляції };

    змінити_в_середовищі(середовище_створення_секції, назва, обʼєкт_компіляції);

    ціль результат_компіляції_тіла_секції = скомпілювати_тіло_секції(К, середовище_секції, дані.тіло);
    якщо результат_компіляції_тіла_секції.помилка != пусто {
      вернути РезультатЗначення { Значення {}, результат_компіляції_тіла_секції.помилка };
    }

    вернути РезультатЗначення { значення, пусто };
  }

  місцева дія скомпілювати_визначити_секцію(К: комірка<Компілятор>, блок: комірка<Блок>, інформація_виконання_шаблону: комірка<ІнформаціяВиконанняШаблону>, середовище: комірка<Середовище>, місцезнаходження: комірка<Місцезнаходження>, дані: комірка<АСДДаніСекція>): комірка<ПомилкаКомпіляції> {
    ціль назва = дані.ідентифікатор.значення;
    ціль результат_отримання_з_середовища = отримати_з_локального_середовища(середовище, назва);
    якщо результат_отримання_з_середовища.знайдено == так {
      ціль субʼєкт = результат_отримання_з_середовища.субʼєкт;
      якщо субʼєкт.вид == ВидОКСекція {
        ціль секція_ = субʼєкт.дані як комірка<Секція>;
        ціль результат_компіляції_тіла_секції = скомпілювати_тіло_секції(К, секція_.середовище, дані.тіло);
        якщо результат_компіляції_тіла_секції.помилка != пусто {
          вернути результат_компіляції_тіла_секції.помилка;
        }
        вернути пусто;
      }
      ціль помилка_компіляції = створити_помилку_компіляції_субʼєкт_вже_визначено(місцезнаходження, назва);
      вернути помилка_компіляції;
    } інакше {
      ціль результат_створення_секції = скомпілювати_секцію(К, пусто, пусто, середовище, місцезнаходження, дані);
      якщо результат_створення_секції.помилка != пусто {
        вернути результат_створення_секції.помилка;
      }
      змінити_в_середовищі(середовище, назва, результат_створення_секції.значення.к);
    }
    вернути пусто;
  }
}
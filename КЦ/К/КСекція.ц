взяти визначення К;

секція ц {
  місцева дія скомпілювати_секцію(К: адреса<Компілятор>, інформація_середовища: ІнформаціяСередовища, місцезнаходження: адреса<Місцезнаходження>, дані: адреса<АСДДаніСекція>): Результат {
    ціль назва = дані.ідентифікатор.значення;
    ціль середовище_компіляції_секції = створити_середовище(інформація_середовища.середовище);
    ціль середовище_секції = створити_середовище(середовище_компіляції_секції);
    ціль обʼєкт_секції = виділити<ОбʼєктСекції>();
    обʼєкт_секції.назва = назва;
    обʼєкт_секції.середовище = середовище_секції;
    середовище_секції.обʼєкт_секції = обʼєкт_секції;
    якщо інформація_середовища.інформація_виконання_шаблону == пусто {
      змінити_в_середовищі(середовище_компіляції_секції, назва_з_ю8сі(К, назва), Обʼєкт { ВидОбʼєктаСекція, дані = обʼєкт_секції });
    } інакше {
      ціль обʼєкт_шаблону = інформація_середовища.інформація_виконання_шаблону.шаблон;
      ціль аргументи = інформація_середовища.інформація_виконання_шаблону.аргументи;
      якщо обʼєкт_шаблону.вид == ВидШаблонуЗвичайний {
        змінити_в_реєстрі<Накопичувач<адреса<ОбʼєктТипу>>, Обʼєкт>(
          обʼєкт_шаблону.реєстр_готових_значень_звичайного_шаблону,
          аргументи,
          Обʼєкт { ВидОбʼєктаСекція, дані = обʼєкт_секції }
        );
      } інакше {
        ціль помилка_компіляції = виділити_помилку_компіляції(місцезнаходження, ю8"Щось пішло не так");
        вернути результат_помилка(помилка_компіляції);
      }
    }
    ціль результат_компіляції_тіла_секції = скомпілювати_тіло_секції(
      К,
      ІнформаціяСередовища {
        середовище = середовище_секції,
        обʼєкт_дії = пусто,
        llvm_block = пусто,
        інформація_виконання_шаблону = інформація_середовища.інформація_виконання_шаблону
      },
      дані.тіло
    );
    якщо результат_компіляції_тіла_секції.помилка != пусто {
      вернути результат_помилка(результат_компіляції_тіла_секції.помилка);
    }
    вернути Результат { Обʼєкт { ВидОбʼєктаСекція, дані = обʼєкт_секції }, помилка = пусто };
  }

  місцева дія скомпілювати_визначити_секцію(К: адреса<Компілятор>, інформація_середовища: ІнформаціяСередовища, місцезнаходження: адреса<Місцезнаходження>, дані: адреса<АСДДаніСекція>): адреса<ПомилкаКомпіляції> {
    ціль назва = дані.ідентифікатор.значення;
    змінна ціль обʼєкт_з_середовища: Обʼєкт;
    ціль успіх_отримання_з_середовища = отримати_з_локального_середовища(інформація_середовища.середовище, назва_з_ю8сі(К, назва), обʼєкт_з_середовища::адреса);
    якщо успіх_отримання_з_середовища == так {
      якщо обʼєкт_з_середовища.вид == ВидОбʼєктаСекція {
        ціль обʼєкт_секції = обʼєкт_з_середовища.дані як адреса<ОбʼєктСекції>;
        ціль результат_компіляції_тіла_секції = скомпілювати_тіло_секції(
          К,
          ІнформаціяСередовища {
            середовище = обʼєкт_секції.середовище,
            обʼєкт_дії = пусто,
            llvm_block = пусто,
            інформація_виконання_шаблону = інформація_середовища.інформація_виконання_шаблону
          },
          дані.тіло
        );
        якщо результат_компіляції_тіла_секції.помилка != пусто {
          вернути результат_компіляції_тіла_секції.помилка;
        }
        вернути пусто;
      }
      ціль помилка_компіляції = виділити_помилку_компіляції_субʼєкт_вже_визначено(місцезнаходження, ю8сі_в_ю8(назва));
      вернути помилка_компіляції;
    } інакше {
      ціль результат_компіляції_секції = скомпілювати_секцію(К, інформація_середовища, місцезнаходження, дані);
      якщо результат_компіляції_секції.помилка != пусто {
        вернути результат_компіляції_секції.помилка;
      }
      змінити_в_середовищі(інформація_середовища.середовище, назва_з_ю8сі(К, назва), результат_компіляції_секції.обʼєкт);
    }
    вернути пусто;
  }
}
взяти визначення К;

секція ц {
  місцева дія скомпілювати_виконати(К: комірка<Компілятор>, інформація_середовища: ІнформаціяСередовища, місцезнаходження: комірка<Місцезнаходження>, дані: комірка<АСДДаніВиконати>): Результат {
    ціль результат_компіляції_значення = скомпілювати_значення(К, інформація_середовища, дані.обʼєкт, пусто);
    якщо результат_компіляції_значення.помилка != пусто {
      вернути Результат { помилка = результат_компіляції_значення.помилка };
    }
    ціль кількість_аргументів = дані.аргументи.довжина;
    ціль аргументи = виділити<Аргумент>(кількість_аргументів як ц32);
    змінна ах: позитивне = 0;
    поки ах < дані.аргументи.довжина {
      ціль асд_аргумент = дані.аргументи.елементи[ах];
      ціль результат_компіляції_значення_аргумента = скомпілювати_значення(К, інформація_середовища, асд_аргумент, пусто);
      якщо результат_компіляції_значення_аргумента.помилка != пусто {
        вернути Результат { помилка = результат_компіляції_значення_аргумента.помилка };
      }
      аргументи[ах] = Аргумент {
        результат_компіляції_значення_аргумента.обʼєкт,
        асд_аргумент.місцезнаходження
      };
      ах = ах + 1;
    }
    вернути обʼєкт_виконати(результат_компіляції_значення.обʼєкт, К, інформація_середовища, місцезнаходження, кількість_аргументів, аргументи);
  }
}
взяти визначення К;

секція цк {
  структура РезультатКомпіляціїПараметраДії {
    параметр: Параметр;
    помилка: комірка<ПомилкаКомпіляції>;
  }

  місцева дія скомпілювати_параметр_дії(К: комірка<Компілятор>, дія_: комірка<Дія>, асд_параметр: комірка<АСДПараметр>): РезультатКомпіляціїПараметраДії {
    ціль результат_компіляції_типу = скомпілювати_значення_як_тип(К, пусто, дія_.середовище_створення_дії, асд_параметр.тип);
    якщо результат_компіляції_типу.помилка != пусто {
      вернути РезультатКомпіляціїПараметраДії { помилка = результат_компіляції_типу.помилка };
    }
    ціль параметр = Параметр {
      назва = асд_параметр.ідентифікатор.значення,
      тип = результат_компіляції_типу.тип
    };
    вернути РезультатКомпіляціїПараметраДії { параметр = параметр, помилка = пусто };
  }

  місцева дія заповнити_неповну_дію(К: комірка<Компілятор>, середовище: комірка<Середовище>, дія_: комірка<Дія>, місцезнаходження: комірка<Місцезнаходження>, дані: комірка<АСДДаніДія>): комірка<ПомилкаКомпіляції> {
    ціль назва = дані.ідентифікатор.значення;
    ціль помилка_компіляції = створити_помилку_компіляції_субʼєкт_вже_визначено(місцезнаходження, назва);
    вернути помилка_компіляції;
  }

  місцева дія скомпілювати_тіло_дії(К: комірка<Компілятор>, дія_: комірка<Дія>, середовище_створення_дії: комірка<Середовище>, тіло: комірка <СписокАСДЗначень>): комірка<ПомилкаКомпіляції> {
    ціль середовище_тіла_дії = створити_середовище(середовище_створення_дії);

    дія_.середовище_тіла_дії = середовище_тіла_дії;

    ціль блок = виділити<Блок>();
    блок.ret_value = пусто;
    блок.alloca_block = LLVM::block(К.Л, дія_.л_функція, ю8"alloca");
    блок.block = LLVM::block(К.Л, дія_.л_функція, ю8"entry");
    блок.exit_block = LLVM::block(К.Л, дія_.л_функція, ю8"exit");
    блок.function_exit_block = блок.exit_block;

    якщо перевірити_тип(К, дія_.тип_результату, К.тип_ніщо) == ні {
      блок.ret_value = LLVM::alloca(К.Л, блок.alloca_block, ю8"return", отримати_ЛТип(К, дія_.тип_результату));
    }

    змінна пх: позитивне = 0;
    поки пх < дія_.параметри.довжина {
      ціль параметр = дія_.параметри.дані[пх];
      ціль arg = LLVM::function_arg(К.Л, дія_.л_функція, пх);
      ціль л_значення = LLVM::alloca(К.Л, блок.alloca_block, параметр.назва, отримати_ЛТип(К, параметр.тип));
      LLVM::store(К.Л, блок.alloca_block, arg, л_значення);
      ціль ціль_ = виділити<Ціль>();
      ціль_.тип = параметр.тип;
      ціль_.л_значення_alloca = л_значення;
      змінити_в_середовищі(середовище_тіла_дії, параметр.назва, ОбʼєктКомпіляції { ВидОКЦіль, ціль_ });
      пх = пх + 1;
    }

    ціль результат_компіляції_блоку = скомпілювати_блок(К, блок, середовище_тіла_дії, тіло);
    якщо результат_компіляції_блоку.помилка != пусто {
      вернути результат_компіляції_блоку.помилка;
    }

    LLVM::br(К.Л, блок.alloca_block, блок.block);
    якщо блок.ret_value == пусто {
      LLVM::ret(К.Л, блок.exit_block, пусто);
    } інакше {
      ціль л_значення = LLVM::load(К.Л, блок.exit_block, отримати_ЛТип(К, дія_.тип_результату), блок.ret_value);
      LLVM::ret(К.Л, блок.exit_block, л_значення);
    }

    вернути пусто;
  }

  місцева дія скомпілювати_дію(К: комірка<Компілятор>, блок: комірка<Блок>, інформація_виконання_шаблону: комірка<ІнформаціяВиконанняШаблону>, середовище: комірка<Середовище>, місцезнаходження: комірка<Місцезнаходження>, дані: комірка<АСДДаніДія>): РезультатКомпіляціїДії {
    ціль середовище_створення_дії = створити_середовище(середовище);

    ціль дія_ = виділити<Дія>();
    дія_.середовище_створення_дії = середовище_створення_дії;
    дія_.назва = дані.ідентифікатор.значення;
    дія_.параметри = виділити_список<Параметр>();
    дія_.тип_результату = К.тип_ніщо;
    дія_.середовище_тіла_дії = пусто;

    ціль л_типи_параметрів = виділити<комірка<ЛТип>>(дані.кількість_параметрів);

    змінна пх: позитивне = 0;
    поки пх < дані.кількість_параметрів {
      ціль асд_параметр = дані.параметри[пх];
      ціль результат_компіляції_параметра = скомпілювати_параметр_дії(К, дія_, асд_параметр);
      якщо результат_компіляції_параметра.помилка != пусто {
        вернути РезультатКомпіляціїДії { помилка = результат_компіляції_параметра.помилка };
      }
      ціль параметр = результат_компіляції_параметра.параметр;
      додати_до_списку<Параметр>(дія_.параметри, параметр);
      л_типи_параметрів[пх] = отримати_ЛТип(К, параметр.тип);
      пх = пх + 1;
    }

    якщо дані.тип_результату != пусто {
      ціль результат_компіляції_типу = скомпілювати_значення_як_тип(К, пусто, середовище_створення_дії, дані.тип_результату);
      якщо результат_компіляції_типу.помилка != пусто {
        вернути РезультатКомпіляціїДії { помилка = результат_компіляції_типу.помилка };
      }
      дія_.тип_результату = результат_компіляції_типу.тип;
    }

    змінна LLVM_LINKAGE: позитивне = 0;
    якщо дані.видимість == АСДВидимістьЗовнішня {
      LLVM_LINKAGE = LLVM::LINKAGE_EXTERNAL;
    } інакше якщо дані.видимість == АСДВидимістьМісцева {
      LLVM_LINKAGE = LLVM::LINKAGE_DSO_LOCAL;
    } інакше {
      LLVM_LINKAGE = LLVM::LINKAGE_INTERNAL;
    }

    якщо strcmp(дія_.назва, ю8"main") == 0 {
      LLVM_LINKAGE = LLVM::LINKAGE_EXTERNAL;
    }

    змінна назва_з_префіксом = дія_.назва;
    ціль префікс = отримати_префікс_назви(середовище_створення_дії);
    якщо префікс != пусто {
      назва_з_префіксом = зʼєднати_ю8(зʼєднати_ю8(префікс, ю8"::"), дія_.назва);
    }

    дія_.л_функція = LLVM::function(К.Л, LLVM_LINKAGE, назва_з_префіксом, отримати_ЛТип(К, дія_.тип_результату), дія_.параметри.довжина, л_типи_параметрів, ні);

    ціль дані_типу_дія = виділити<ДаніТипуДія>();
    дані_типу_дія.л_тип_функції = LLVM::type_of_function(К.Л, дія_.л_функція);
    дані_типу_дія.параметри = дія_.параметри;
    дані_типу_дія.тип_результату = дія_.тип_результату;

    дія_.тип = Тип { ВидТипуДія, дані = дані_типу_дія };

    якщо інформація_виконання_шаблону == пусто {
      змінити_в_середовищі(середовище_створення_дії, дія_.назва, ОбʼєктКомпіляції { ВидОКДія, дія_ });
    } інакше {
      ціль шаблон = інформація_виконання_шаблону.шаблон;
      ціль аргументи = інформація_виконання_шаблону.аргументи;
      ціль карта_готових_значень = шаблон.карта_готових_значень;

      змінити_значення_карти<комірка<список<Значення>>, Значення>(карта_готових_значень, аргументи, Значення { ВидЗначенняОК, к = ОбʼєктКомпіляції { ВидОКДія, дія_ } });
    }

    якщо дані.тіло != пусто {
      ціль помилка_компіляції_тіла = скомпілювати_тіло_дії(К, дія_, середовище_створення_дії, дані.тіло);
      якщо помилка_компіляції_тіла != пусто {
        вернути РезультатКомпіляціїДії { помилка = помилка_компіляції_тіла };
      }
    }

    вернути РезультатКомпіляціїДії { дія_ = дія_, помилка = пусто };
  }

  місцева дія скомпілювати_визначити_дію(К: комірка<Компілятор>, блок: комірка<Блок>, інформація_виконання_шаблону: комірка<ІнформаціяВиконанняШаблону>, середовище: комірка<Середовище>, місцезнаходження: комірка<Місцезнаходження>, дані: комірка<АСДДаніДія>): комірка<ПомилкаКомпіляції> {
    ціль назва = дані.ідентифікатор.значення;
    ціль результат_отримання_з_середовища = отримати_з_локального_середовища(середовище, назва);
    якщо результат_отримання_з_середовища.знайдено == так {
      ціль субʼєкт = результат_отримання_з_середовища.субʼєкт;
      якщо субʼєкт.вид == ВидОКДія {
        ціль дія_ = субʼєкт.дані як комірка<Дія>;
        якщо дія_.середовище_тіла_дії == пусто {
          ціль помилка_заповнення = заповнити_неповну_дію(К, середовище, дія_, місцезнаходження, дані);
          якщо помилка_заповнення != пусто {
            вернути помилка_заповнення;
          }
          вернути пусто;
        }
      }
      ціль помилка_компіляції = створити_помилку_компіляції_субʼєкт_вже_визначено(місцезнаходження, назва);
      вернути помилка_компіляції;
    } інакше {
      ціль результат_створення_дії = скомпілювати_дію(К, пусто, пусто, середовище, місцезнаходження, дані);
      якщо результат_створення_дії.помилка != пусто {
        вернути результат_створення_дії.помилка;
      }
      змінити_в_середовищі(середовище, назва, ОбʼєктКомпіляції { ВидОКДія, дані = результат_створення_дії.дія_ });
    }
    вернути пусто;
  }
}
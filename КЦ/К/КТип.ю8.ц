взяти визначення К;

секція ц {
  місцева дія скомпілювати_тип_варіація(К: адреса<Компілятор>, інформація_середовища: ІнформаціяСередовища, місцезнаходження: адреса<Місцезнаходження>, дані: адреса<АСДДаніТипВаріація>): РезультатЯкТип {
    ціль типи_варіації = зробити_накопичувач<адреса<ОбʼєктТипу>>();
    змінна найбільший_тип_варіації: адреса<ОбʼєктТипу> = пусто;
    змінна розмір_найбільшого_типу_варіації: позитивне = 0;
    змінна пх: позитивне = 0;
    поки пх < дані.типи.довжина {
      ціль результат_компіляції_типу = скомпілювати_значення_як_тип(К, інформація_середовища, дані.типи.елементи[пх], так);
      якщо результат_компіляції_типу.помилка != пусто {
        вернути РезультатЯкТип { помилка = результат_компіляції_типу.помилка, ... };
      }
      покласти_в_накопичувач<адреса<ОбʼєктТипу>>(типи_варіації::адреса, результат_компіляції_типу.обʼєкт_типу);
      ціль розмір_типу = дізнатись_розмір_типу(К, результат_компіляції_типу.обʼєкт_типу);
      якщо розмір_типу > розмір_найбільшого_типу_варіації {
        найбільший_тип_варіації = результат_компіляції_типу.обʼєкт_типу;
        розмір_найбільшого_типу_варіації = розмір_типу;
      }
      пх = пх + 1;
    }
    ціль обʼєкт_типу = виділити_обʼєкт_типу(ВидТипуВаріація);
    обʼєкт_типу.типи_варіації = типи_варіації;
    обʼєкт_типу.найбільший_тип_варіації = найбільший_тип_варіації;
    обʼєкт_типу.llvm_type = найбільший_тип_варіації.llvm_type;
    вернути РезультатЯкТип { обʼєкт_типу, помилка = пусто };
  }

  місцева дія скомпілювати_тип_дії(К: адреса<Компілятор>, інформація_середовища: ІнформаціяСередовища, місцезнаходження: адреса<Місцезнаходження>, дані: адреса<АСДДаніТипДії>): РезультатЯкТип {
    ціль обʼєкт_типу = виділити_обʼєкт_типу(ВидТипуДія);
    обʼєкт_типу.параметри_дії = зробити_накопичувач<ПараметрДії>();
    обʼєкт_типу.тип_результату_дії = К.тип_ніщо;
    ціль llvm_param_types = виділити<адреса<LLVM::Type>>(дані.кількість_параметрів);
    змінна пх: позитивне = 0;
    поки пх < дані.кількість_параметрів {
      ціль параметр = дані.параметри[пх];
      змінна назва_параметра = ю8 { 0, пусто };
      якщо параметр.ідентифікатор != пусто {
        назва_параметра = ю8 { позитивне(параметр.ідентифікатор.розмір_значення), параметр.ідентифікатор.значення };
      }
      ціль результат_компіляції_значення_типу_параметра = скомпілювати_значення_як_тип(
        К,
        інформація_середовища,
        параметр.тип,
        так
      );
      якщо результат_компіляції_значення_типу_параметра.помилка != пусто {
        вернути РезультатЯкТип { помилка = результат_компіляції_значення_типу_параметра.помилка, ... };
      }
      ціль обʼєкт_типу_параметра = результат_компіляції_значення_типу_параметра.обʼєкт_типу;
      ціль параметр_дії = ПараметрДії { назва = назва_з_ю8(К, назва_параметра), тип = обʼєкт_типу_параметра };
      покласти_в_накопичувач<ПараметрДії>(обʼєкт_типу.параметри_дії::адреса, параметр_дії);
      llvm_param_types[пх] = обʼєкт_типу_параметра.llvm_type;
      пх = пх + 1;
    }
    якщо дані.тип_результату != пусто {
      ціль результат_компіляції_значення_типу_результату = скомпілювати_значення_як_тип(
        К,
        інформація_середовища,
        дані.тип_результату,
        так
      );
      якщо результат_компіляції_значення_типу_результату.помилка != пусто {
        вернути РезультатЯкТип { помилка = результат_компіляції_значення_типу_результату.помилка, ... };
      }
      обʼєкт_типу.тип_результату_дії = результат_компіляції_значення_типу_результату.обʼєкт_типу;
    }
    обʼєкт_типу.llvm_function_type = LLVM::function_type(К.L, обʼєкт_типу.тип_результату_дії.llvm_type, дані.кількість_параметрів, llvm_param_types, 0);
    обʼєкт_типу.llvm_type = LLVM::pointer(К.L);
    вернути РезультатЯкТип { обʼєкт_типу, помилка = пусто };
  }

  місцева дія скомпілювати_тип_масиву(К: адреса<Компілятор>, інформація_середовища: ІнформаціяСередовища, місцезнаходження: адреса<Місцезнаходження>, дані: адреса<АСДДаніТипМасиву>): РезультатЯкТип {
    ціль помилка_компіляції = виділити_помилку_компіляції(місцезнаходження, ю8"Не втілено");
    вернути РезультатЯкТип { помилка = помилка_компіляції, ... };
  }
}
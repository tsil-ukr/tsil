взяти визначення К;

зовнішня дія перевірити_чи_файл_існує(шлях: памʼять<п8>): позитивне;
зовнішня дія перевірити_чи_файл_доступний(шлях: памʼять<п8>): позитивне;
зовнішня дія прочитати_файл(шлях: памʼять<п8>): памʼять<п8>;
зовнішня дія отримати_шлях_до_папки_файлу(шлях: памʼять<п8>): памʼять<п8>;
зовнішня дія вирівняти_шлях(шлях: памʼять<п8>): памʼять<п8>;

секція ц {
  місцева дія скомпілювати_взяти(К: адреса<Компілятор>, інформація_середовища: ІнформаціяСередовища, місцезнаходження: адреса<Місцезнаходження>, дані: адреса<АСДДаніВзяти>): адреса<ПомилкаКомпіляції> {
    якщо дані.тип != пусто {
      якщо перевірити_чи_ю8_рівні(ю8 { дані.тип.значення, дані.тип.розмір_значення }, ю8"визначення") {
        ціль ю8_буфер_шляху = створити_буфер_ю8();
        якщо дані.відносно == 1 {
          ціль текст_коду = К.історія_взяття.дані[К.історія_взяття.розмір - 1];
          ціль шлях_до_папки = отримати_шлях_до_папки_файлу(текст_коду.шлях);
          додати_до_буфера_ю8(ю8_буфер_шляху, шлях_до_папки);
          звільнити(шлях_до_папки як адреса);
        } інакше {
          додати_ю8_до_буфера_ю8(ю8_буфер_шляху, К.cwd);
        }
        додати_ю8_до_буфера_ю8(ю8_буфер_шляху, ю8"/");
        змінна ціль назва_файлу: памʼять<п8> = пусто;
        змінна ціль пх: позитивне = 0;
        поки пх < дані.довжина_шляху {
          додати_до_буфера_ю8(ю8_буфер_шляху, дані.шлях[пх].значення);
          якщо пх != дані.довжина_шляху - 1 {
            додати_ю8_до_буфера_ю8(ю8_буфер_шляху, ю8"/");
          }
          якщо пх == дані.довжина_шляху - 1 {
            назва_файлу = дані.шлях[пх].значення;
          }
          пх = пх + 1;
        }
        додати_ю8_до_буфера_ю8(ю8_буфер_шляху, ю8".в");
        ціль ю8_шляху_до_файлу = створити_ю8_сі_з_буфера(ю8_буфер_шляху);
        видалити_останнє_значення_з_буфера_ю8(ю8_буфер_шляху);
        видалити_останнє_значення_з_буфера_ю8(ю8_буфер_шляху);
        видалити_останнє_значення_з_буфера_ю8(ю8_буфер_шляху);
        додати_ю8_до_буфера_ю8(ю8_буфер_шляху, ю8"/");
        додати_до_буфера_ю8(ю8_буфер_шляху, назва_файлу);
        додати_ю8_до_буфера_ю8(ю8_буфер_шляху, ю8".в");
        ціль ю8_шляху_до_папки_і_файлу = створити_ю8_сі_з_буфера(ю8_буфер_шляху);
        звільнити_буфер_ю8(ю8_буфер_шляху);
        змінна ціль шлях_до_файлу_визначення: памʼять<п8> = пусто;
//        біб::друк_ю8(ю8_шляху_до_файлу);
//        біб::друк_ю8(ю8_шляху_до_папки_і_файлу);
        якщо перевірити_чи_файл_доступний(ю8_шляху_до_файлу) != 0 {
          шлях_до_файлу_визначення = ю8_шляху_до_файлу;
        } інакше якщо перевірити_чи_файл_доступний(ю8_шляху_до_папки_і_файлу) != 0 {
          шлях_до_файлу_визначення = ю8_шляху_до_папки_і_файлу;
        }
        якщо шлях_до_файлу_визначення == пусто {
          ціль помилка_компіляції = виділити_помилку_компіляції(місцезнаходження, ю8"Файл визначення не знайдено");
          вернути помилка_компіляції;
        }
        шлях_до_файлу_визначення = вирівняти_шлях(шлях_до_файлу_визначення);
        пх = 0;
        поки пх < К.взяті_файли.розмір {
          якщо перевірити_чи_ю8_рівно_ю8сі(К.взяті_файли.дані[пх], шлях_до_файлу_визначення) {
            вернути пусто;
          }
          пх = пх + 1;
        }
//        біб::вивести_ю8(ю8сі"беремо ");
//        біб::друк_ю8(шлях_до_файлу_визначення);
        покласти_в_накопичувач<ю8>(К.взяті_файли::адреса, ю8сі_в_ю8(шлях_до_файлу_визначення));
        ціль дані_файлу_визначення = прочитати_файл(шлях_до_файлу_визначення);
        ціль текст_коду = виділити<ТекстКоду>();
        текст_коду.шлях = шлях_до_файлу_визначення;
        текст_коду.розмір_шляху = strlen(шлях_до_файлу_визначення);
        текст_коду.значення = дані_файлу_визначення;
        текст_коду.розмір_значення = strlen(дані_файлу_визначення);
        ціль результат_розбору_цілі = розібрати_ціль(текст_коду);
        якщо результат_розбору_цілі.успіх == 0 {
          ціль помилка_компіляції = виділити_помилку_компіляції(результат_розбору_цілі.помилка.місцезнаходження, ю8 { результат_розбору_цілі.помилка.повідомлення, результат_розбору_цілі.помилка.розмір_повідомлення });
          вернути помилка_компіляції;
        }
        покласти_в_накопичувач<адреса<ТекстКоду>>(К.історія_взяття::адреса, текст_коду);
        ціль результат_компіляції_секції = скомпілювати_тіло_секції(
          К,
          ц::ІнформаціяСередовища {
            середовище = інформація_середовища.середовище,
            обʼєкт_дії = пусто,
            llvm_block = пусто,
            інформація_виконання_шаблону = пусто
          },
          результат_розбору_цілі.тіло
        );
        К.історія_взяття.розмір = К.історія_взяття.розмір - 1;
        вернути результат_компіляції_секції.помилка;
      } інакше якщо перевірити_чи_ю8_рівні(ю8 { дані.тип.значення, дані.тип.розмір_значення }, ю8"біб") {
        //
      }
    }
    ціль помилка_компіляції = виділити_помилку_компіляції(місцезнаходження, ю8"Не вдалося скомпілювати взяти");
    вернути помилка_компіляції;
  }
}
взяти визначення К;

секція цк {
  місцева дія скомпілювати_значення_отримати_зі_секції(К: комірка<Компілятор>, блок: комірка<Блок>, інформація_виконання_шаблону: комірка<ІнформаціяВиконанняШаблону>, середовище: комірка<Середовище>, місцезнаходження: комірка<Місцезнаходження>, дані: комірка<АСДДаніОтриматиЗіСекції>): РезультатЗначення {
    ціль результат_компіляції_обʼєкта_значення = скомпілювати_значення(К, блок, інформація_виконання_шаблону, середовище, дані.обʼєкт);
    якщо результат_компіляції_обʼєкта_значення.помилка != пусто {
      вернути РезультатЗначення { Значення {}, результат_компіляції_обʼєкта_значення.помилка };
    }
    ціль значення = результат_компіляції_обʼєкта_значення.значення;
    якщо значення.вид != ВидЗначенняОК {
      ціль помилка_компіляції = створити_помилку_компіляції(дані.обʼєкт.місцезнаходження, ю8"Значення не є обʼєктом");
      вернути РезультатЗначення { Значення {}, помилка_компіляції };
    }
    ціль ок = значення.к;
    якщо ок.вид != ВидОКСекція {
      ціль помилка_компіляції = створити_помилку_компіляції(дані.обʼєкт.місцезнаходження, ю8"Обʼєкт не є секцією");
      вернути РезультатЗначення { Значення {}, помилка_компіляції };
    }
    ціль назва = дані.ідентифікатор.значення;
    ціль сек = ок.дані як комірка<Секція>;
    ціль резльтат_отримання_з_середовища = отримати_з_локального_середовища(сек.середовище, назва);
    якщо резльтат_отримання_з_середовища.знайдено == ні {
      ціль створити_помилку_компіляції = створити_помилку_компіляції(місцезнаходження, ю8"Субʼєкт не визначено");
      вернути РезультатЗначення { Значення {}, створити_помилку_компіляції };
    }
    вернути РезультатЗначення { Значення { ВидЗначенняОК, к = резльтат_отримання_з_середовища.субʼєкт }, пусто };
  }
}
взяти визначення К;

секція ц {
  місцева дія заповнити_структуру(К: комірка<Компілятор>, обʼєкт_типу: комірка<ОбʼєктТипу>, інформація_середовища: ІнформаціяСередовища, дані: комірка<АСДДаніСтруктура>): комірка<ПомилкаКомпіляції> {
    ціль llvm_param_types = виділити<комірка<LLVM::Type>>(дані.кількість_параметрів);
    змінна пх: позитивне = 0;
    поки пх < дані.кількість_параметрів {
      ціль параметр = дані.параметри[пх];
      ціль назва_параметра = параметр.ідентифікатор.значення;
      ціль результат_компіляції_значення_типу_параметра = скомпілювати_значення_як_тип(К, інформація_середовища, параметр.тип);
      якщо результат_компіляції_значення_типу_параметра.помилка != пусто {
        вернути результат_компіляції_значення_типу_параметра.помилка;
      }
      ціль обʼєкт_типу_параметра = результат_компіляції_значення_типу_параметра.обʼєкт_типу;
      ціль властивість_структури = ВластивістьСтруктури { назва = назва_параметра, тип = обʼєкт_типу_параметра };
      додати_до_списку<ВластивістьСтруктури>(обʼєкт_типу.властивості_структури, властивість_структури);
      llvm_param_types[пх] = обʼєкт_типу_параметра.llvm_type;
      пх = пх + 1;
    }
    LLVM::struct_set_fields(К.L, обʼєкт_типу.llvm_type, дані.кількість_параметрів, llvm_param_types);
    звільнити(llvm_param_types);
    вернути пусто;
  }

  місцева дія скомпілювати_структуру(К: комірка<Компілятор>, інформація_середовища: ІнформаціяСередовища, місцезнаходження: комірка<Місцезнаходження>, дані: комірка<АСДДаніСтруктура>): Результат {
    ціль назва = дані.ідентифікатор.значення;
    ціль середовище_компіляції_структури = створити_середовище(інформація_середовища.середовище);
    змінна обʼєкт_типу: комірка<ОбʼєктТипу>;
    обʼєкт_типу = виділити_обʼєкт_типу(ВидТипуСтруктура);
    обʼєкт_типу.назва = назва;
    обʼєкт_типу.властивості_структури = виділити_список<ВластивістьСтруктури>();
    ціль повна_назва = отримати_повну_назву(інформація_середовища.середовище, назва);
    обʼєкт_типу.llvm_type = LLVM::struct(К.L, повна_назва);
    якщо інформація_середовища.інформація_виконання_шаблону == пусто {
      змінити_в_середовищі(середовище_компіляції_структури, назва, Обʼєкт { ВидОбʼєктаТипу, дані = обʼєкт_типу });
    } інакше {
      ціль обʼєкт_шаблону = інформація_середовища.інформація_виконання_шаблону.шаблон;
      ціль аргументи = інформація_середовища.інформація_виконання_шаблону.аргументи;
      ціль карта_готових_значень = обʼєкт_шаблону.карта_готових_значень;
      змінити_значення_карти<комірка<список<комірка<ОбʼєктТипу>>>, Обʼєкт>(
        карта_готових_значень,
        аргументи,
        Обʼєкт { ВидОбʼєктаТипу, дані = обʼєкт_типу }
      );
    }
    ціль помилка_заповнення = заповнити_структуру(К, обʼєкт_типу, інформація_середовища, дані);
    якщо помилка_заповнення != пусто {
      вернути Результат { помилка = помилка_заповнення };
    }
    ціль обʼєкт = Обʼєкт { ВидОбʼєктаТипу, дані = обʼєкт_типу };
    вернути Результат { обʼєкт, пусто };
  }

  місцева дія скомпілювати_визначити_структуру(К: комірка<Компілятор>, інформація_середовища: ІнформаціяСередовища, місцезнаходження: комірка<Місцезнаходження>, дані: комірка<АСДДаніСтруктура>): комірка<ПомилкаКомпіляції> {
    ціль назва = дані.ідентифікатор.значення;
    змінна обʼєкт_з_середовища: Обʼєкт;
    ціль успіх_отримання_з_середовища = отримати_з_локального_середовища(інформація_середовища.середовище, назва, комірка(обʼєкт_з_середовища));
    якщо успіх_отримання_з_середовища == так {
      якщо обʼєкт_з_середовища.вид == ВидОбʼєктаТипу {
        ціль обʼєкт_типу = обʼєкт_з_середовища.дані як комірка<ОбʼєктТипу>;
        якщо обʼєкт_типу.вид == ВидТипуСтруктура {
          якщо обʼєкт_типу.властивості_структури.довжина == 0 {
            ціль помилка_заповнення = заповнити_структуру(К, обʼєкт_типу, інформація_середовища, дані);
            якщо помилка_заповнення != пусто {
              вернути помилка_заповнення;
            }
            вернути пусто;
          }
        }
      }
      ціль помилка_компіляції = виділити_помилку_компіляції(місцезнаходження, ю8"Субʼєкт вже визначено");
      вернути помилка_компіляції;
    } інакше {
      ціль результат_компіляції_структури = скомпілювати_структуру(К, інформація_середовища, місцезнаходження, дані);
      якщо результат_компіляції_структури.помилка != пусто {
        вернути результат_компіляції_структури.помилка;
      }
      змінити_в_середовищі(інформація_середовища.середовище, назва, результат_компіляції_структури.обʼєкт);
    }
    вернути пусто;
  }
}
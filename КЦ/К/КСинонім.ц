взяти визначення К;

секція цк {
  місцева дія скомпілювати_синонім(К: комірка<Компілятор>, блок: комірка<Блок>, інформація_виконання_шаблону: комірка<ІнформаціяВиконанняШаблону>, середовище: комірка<Середовище>, місцезнаходження: комірка<Місцезнаходження>, дані: комірка<АСДДаніСинонім>): РезультатЗначення {
    ціль назва = дані.ідентифікатор.значення;

    ціль середовище_створення_синоніма = створити_середовище(середовище);

    ціль синонім_ = виділити<Синонім>();
    синонім_.зовнішнє_середовище = середовище_створення_синоніма;
    синонім_.асд_значення = дані.значення;

    змінити_в_середовищі(середовище_створення_синоніма, назва, ОбʼєктКомпіляції { ВидОКСинонім, синонім_ });

    ціль обʼєкт_компіляції = ОбʼєктКомпіляції { ВидОКСинонім, синонім_ };
    ціль значення = Значення { ВидЗначенняОК, к = обʼєкт_компіляції };

    вернути РезультатЗначення { значення, пусто };
  }

  місцева дія скомпілювати_визначити_синонім(К: комірка<Компілятор>, блок: комірка<Блок>, інформація_виконання_шаблону: комірка<ІнформаціяВиконанняШаблону>, середовище: комірка<Середовище>, місцезнаходження: комірка<Місцезнаходження>, дані: комірка<АСДДаніСинонім>): комірка<ПомилкаКомпіляції> {
    ціль назва = дані.ідентифікатор.значення;
    ціль результат_отримання_з_середовища = отримати_з_локального_середовища(середовище, назва);
    якщо результат_отримання_з_середовища.знайдено == так {
      ціль помилка_компіляції = створити_помилку_компіляції_субʼєкт_вже_визначено(місцезнаходження, назва);
      вернути помилка_компіляції;
    } інакше {
      ціль результат_створення_синоніма = скомпілювати_синонім(К, пусто, пусто, середовище, місцезнаходження, дані);
      якщо результат_створення_синоніма.помилка != пусто {
        вернути результат_створення_синоніма.помилка;
      }
      змінити_в_середовищі(середовище, назва, результат_створення_синоніма.значення.к);
    }
    вернути пусто;
  }
}

секція ц {
  місцева дія скомпілювати_визначити_синонім(К: комірка<Компілятор>, блок: комірка<Блок>, інформація_виконання_шаблону: комірка<ІнформаціяВиконанняШаблону>, середовище: комірка<Середовище>, місцезнаходження: комірка<Місцезнаходження>, дані: комірка<АСДДаніСинонім>): комірка<ПомилкаКомпіляції> {
    // ...
  }
}
взяти визначення К;

секція ц {
  місцева дія скомпілювати_синонім(К: адреса<Компілятор>, інформація_середовища: ІнформаціяСередовища, місцезнаходження: адреса<Місцезнаходження>, дані: адреса<АСДДаніСинонім>): Результат {
    ціль назва = ю8 { дані.ідентифікатор.значення, позитивне(дані.ідентифікатор.розмір_значення) };
    ціль середовище_компіляції_синоніма = створити_середовище(інформація_середовища.середовище);
    ціль результат_компіляції_значення = скомпілювати_значення(
      К,
      ІнформаціяСередовища {
        середовище = середовище_компіляції_синоніма,
        обʼєкт_дії = інформація_середовища.обʼєкт_дії,
        llvm_block = інформація_середовища.llvm_block,
        інформація_виконання_шаблону = інформація_середовища.інформація_виконання_шаблону
      },
      дані.значення,
      пусто
    );
    якщо результат_компіляції_значення.помилка != пусто {
      вернути результат_помилка(результат_компіляції_значення.помилка);
    }
    якщо інформація_середовища.інформація_виконання_шаблону == пусто {
      змінити_в_середовищі(середовище_компіляції_синоніма, назва_з_ю8(К, назва), результат_компіляції_значення.обʼєкт);
    } інакше {
      ціль обʼєкт_шаблону = інформація_середовища.інформація_виконання_шаблону.шаблон;
      ціль аргументи = інформація_середовища.інформація_виконання_шаблону.аргументи;
      якщо обʼєкт_шаблону.вид == ВидШаблонуЗвичайний {
        змінити_в_реєстрі<Накопичувач<адреса<ОбʼєктТипу>>, Обʼєкт>(
          обʼєкт_шаблону.реєстр_готових_значень_звичайного_шаблону,
          аргументи,
          результат_компіляції_значення.обʼєкт
        );
      } інакше {
        ціль помилка_компіляції = виділити_помилку_компіляції(місцезнаходження, ю8"Щось пішло не так");
        вернути результат_помилка(помилка_компіляції);
      }
    }
    вернути результат_компіляції_значення;
  }

  місцева дія скомпілювати_визначити_синонім(К: адреса<Компілятор>, інформація_середовища: ІнформаціяСередовища, місцезнаходження: адреса<Місцезнаходження>, дані: адреса<АСДДаніСинонім>): адреса<ПомилкаКомпіляції> {
    ціль назва = ю8 { дані.ідентифікатор.значення, позитивне(дані.ідентифікатор.розмір_значення) };
    змінна ціль обʼєкт_з_середовища: Обʼєкт;
    ціль успіх_отримання_з_середовища = отримати_з_локального_середовища(інформація_середовища.середовище, назва_з_ю8(К, назва), обʼєкт_з_середовища::адреса);
    якщо успіх_отримання_з_середовища == так {
      ціль помилка_компіляції = виділити_помилку_компіляції_субʼєкт_вже_визначено(місцезнаходження, назва);
      вернути помилка_компіляції;
    } інакше {
      ціль результат_компіляції_синоніма = скомпілювати_синонім(К, інформація_середовища, місцезнаходження, дані);
      якщо результат_компіляції_синоніма.помилка != пусто {
        вернути результат_компіляції_синоніма.помилка;
      }
      змінити_в_середовищі(інформація_середовища.середовище, назва_з_ю8(К, назва), результат_компіляції_синоніма.обʼєкт);
    }
    вернути пусто;
  }
}
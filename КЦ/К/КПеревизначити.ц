взяти визначення К;

секція ц {
  місцева дія скомпілювати_перевизначити(К: комірка<Компілятор>, л_блок: комірка<ЛБлок>, середовище: комірка<Середовище>, місцезнаходження: комірка<Місцезнаходження>, дані: комірка<АСДДаніПеревизначити>): РезультатКомпіляціїПеревизначити {
    ціль назва = дані.ідентифікатор.значення;

    ціль результат_отримання_з_середовища = отримати_з_середовища(середовище, назва);
    якщо результат_отримання_з_середовища.знайдено == ні {
      ціль помилка_компіляції = створити_помилку_компіляції(пусто, ю8"Субʼєкт не визначено");
      вернути РезультатКомпіляціїПеревизначити { помилка_компіляції };
    } інакше {
      ціль субʼєкт = результат_отримання_з_середовища.субʼєкт;
      якщо субʼєкт.вид == ВидСубʼєктаЦіль {
        ціль обʼєкт_цілі = субʼєкт.дані як комірка<ОбʼєктЦілі>;

        ціль результат_компіляції_як_значення = скомпілювати_асд_значення_як_значення(К, л_блок, середовище, дані.значення, так);
        якщо результат_компіляції_як_значення.помилка != пусто {
          вернути РезультатКомпіляціїПеревизначити { результат_компіляції_як_значення.помилка };
        }

        якщо перевірити_тип(К, результат_компіляції_як_значення.тип, обʼєкт_цілі.тип) == ні {
          ціль помилка_компіляції = створити_помилку_компіляції(пусто, ю8"Типи не співпадають");
          вернути РезультатКомпіляціїПеревизначити { помилка_компіляції };
        }

        л::store(К.Л, л_блок, результат_компіляції_як_значення.л_значення, обʼєкт_цілі.л_значення);
      }
    }

    вернути РезультатКомпіляціїПеревизначити { пусто };
  }
}
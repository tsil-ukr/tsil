взяти визначення ./КЦ;

секція КЦ {
  місцева дія змішати_шлях_для_реєстру(ключ: Шлях, аргумент: невідома_адреса): позитивне {
    змінна змішане: позитивне = 5381;
    змінна п: позитивне = 0;

    поки п < ключ.розмір {
      змішане = (змішане * 33) + позитивне(ключ.дані[п]);
      п += 1;
    }

    вернути змішане;
  }

  місцева дія перевірити_шляхи_в_реєстрі(а: Шлях, б: Шлях, аргумент: невідома_адреса): логічне {
    якщо а.розмір != б.розмір {
      вернути ні;
    }

    змінна п: позитивне = 0;

    поки п < а.розмір {
      якщо а.дані[п] != б.дані[п] {
        вернути ні;
      }

      п += 1;
    }

    вернути так;
  }

  місцева дія клонувати_шлях(компілятор: адреса<Компілятор>, шлях: Шлях): Шлях {
    змінна значення_копії = Шлях { шлях.розмір, виділити_памʼять<п8>(компілятор, шлях.розмір) };

    змінна п: позитивне = 0;
    поки п < шлях.розмір {
      значення_копії.дані[п] = шлях.дані[п];

      п += 1;
    }

    вернути значення_копії;
  }

  місцева дія знищити_шлях(компілятор: адреса<Компілятор>, шлях: Шлях) {
    звільнити_памʼять(компілятор, шлях.дані);
  }
}
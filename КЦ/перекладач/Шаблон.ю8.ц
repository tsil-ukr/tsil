взяти визначення КЦ;

секція КЦ {
  зовнішня дія скомпілювати_параметри_шаблону(компілятор: адреса<Компілятор>, контекст: Контекст, параметри_розбирача: розбирач::Параметри, місцезнаходження: розбирач::Місцезнаходження): РезультатПараметрів {
    змінна параметри = Параметри { 0, пусто, 0 };

    змінна п: позитивне = 0;

    поки п < параметри_розбирача.розмір {
      змінна параметр_розбирача = параметри_розбирача.дані[п];

      змінна назва_ю8 = розбирач::отримати_значення_слова(
        ю8 { контекст.розібраний_текст.текст_коду.значення.розмір, контекст.розібраний_текст.текст_коду.значення.дані },
        контекст.розібраний_текст.слова.дані[параметр_розбирача.ідентифікатор.позиція_слова]
      );
      змінна назва_т8 = отримати_т8_з_ю8(компілятор, назва_ю8);

      якщо параметр_розбирача.тип == пусто {
        покласти_параметр(компілятор, параметри::адреса, Параметр { назва_копія(назва_т8), пусто });
      } інакше {
        змінна результат_типу = скомпілювати_значення_як_тип(компілятор, контекст, параметр_розбирача.тип, так);
        якщо результат_типу.помилка != пусто {
          звільнити_памʼять(компілятор, назва_т8.дані);
          звільнити_параметри(компілятор, параметри);

          вернути РезультатПараметрів { ..., помилка = результат_типу.помилка };
        }

        покласти_параметр(компілятор, параметри::адреса, Параметр { назва_копія(назва_т8), результат_типу.обʼєкт_типу });
      }

      п += 1;
    }

    вернути РезультатПараметрів {
      параметри,
      помилка = пусто
    };
  }

  зовнішня дія скомпілювати_визначити_шаблон(компілятор: адреса<Компілятор>, контекст: Контекст, дані_шаблону: розбирач::ДаніСполукиШаблон, місцезнаходження: розбирач::Місцезнаходження): РезультатОбʼєктаШаблону {
    змінна назва_ю8 = розбирач::отримати_значення_слова(
      ю8 { контекст.розібраний_текст.текст_коду.значення.розмір, контекст.розібраний_текст.текст_коду.значення.дані },
      контекст.розібраний_текст.слова.дані[дані_шаблону.ідентифікатор.значення.позиція_слова]
    );
    змінна назва_т8 = отримати_т8_з_ю8(компілятор, назва_ю8);

    змінна обʼєкт_з_середовища: Обʼєкт;
    якщо знайти_в_середовищі(контекст.середовище, назва_зовнішня(назва_т8), обʼєкт_з_середовища::адреса) {
      звільнити_памʼять(компілятор, назва_т8.дані);

      вернути РезультатОбʼєктаШаблону {
        ...,
        помилка = створити_помилку(
          компілятор,
          "Субʼєкт вже визначено.",
          створити_місцезнаходження_помилки_з_позиції_слова(компілятор, контекст.розібраний_текст, дані_шаблону.ідентифікатор.значення.позиція_слова)
        )
      };
    }

    змінна результат_параметрів = скомпілювати_параметри_шаблону(компілятор, контекст, дані_шаблону.параметри, місцезнаходження);
    якщо результат_параметрів.помилка != пусто {
      вернути РезультатОбʼєктаШаблону {
        ...,
        помилка = результат_параметрів.помилка
      };
    }

    змінна обʼєкт_шаблону = виділити_обʼєкт_шаблону(компілятор);

    обʼєкт_шаблону.назва = назва_копія(назва_т8);
    обʼєкт_шаблону.параметри = результат_параметрів.параметри;
    обʼєкт_шаблону.сполука = дані_шаблону.значення;
    обʼєкт_шаблону.контекст_компіляції = контекст;

    змінити_в_середовищі(контекст.середовище, назва_копія(назва_т8), Обʼєкт { ВидОбʼєктаШаблон, обʼєкт_шаблону });

    вернути РезультатОбʼєктаШаблону {
      обʼєкт_шаблону,
      помилка = пусто
    };
  }
}
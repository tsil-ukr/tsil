взяти визначення КЦ;

секція КЦ {
  зовнішня дія скомпілювати_параметри_шаблону(компілятор: адреса<Компілятор>, контекст: Контекст, параметри_розбирача: розбирач::ПараметриШаблону, місцезнаходження: розбирач::Місцезнаходження): РезультатПараметрів {
    змінна параметри = Параметри { 0, пусто, 0 };

    змінна п: позитивне = 0;

    поки п < параметри_розбирача.розмір {
      змінна параметр_розбирача = параметри_розбирача.дані[п];

      змінна назва_т8 = розбирач::отримати_значення_слова(
        т8 { контекст.розібраний_текст.текст_коду.значення.розмір, контекст.розібраний_текст.текст_коду.значення.дані },
        контекст.розібраний_текст.слова.дані[параметр_розбирача.ідентифікатор.позиція_слова]
      );

      якщо параметр_розбирача.тип == пусто {
        покласти_параметр(
          компілятор,
          параметри::адреса,
          Параметр {
            назва_зовнішня(назва_т8),
            пусто,
            Місцезнаходження { контекст.розібраний_текст, параметр_розбирача.ідентифікатор.позиція_слова },
            Місцезнаходження { пусто, 0 }
          }
        );
      } інакше {
        змінна результат_типу = скомпілювати_значення_як_тип(компілятор, контекст, параметр_розбирача.тип, так);
        якщо результат_типу.помилка != пусто {
          звільнити_параметри(компілятор, параметри);

          вернути РезультатПараметрів { ..., помилка = результат_типу.помилка };
        }

        покласти_параметр(
          компілятор,
          параметри::адреса,
          Параметр {
            назва_зовнішня(назва_т8),
            результат_типу.обʼєкт_типу,
            Місцезнаходження { контекст.розібраний_текст, параметр_розбирача.ідентифікатор.позиція_слова },
            Місцезнаходження { контекст.розібраний_текст, параметр_розбирача.тип.місцезнаходження.позиція_початкового_слова }
          }
        );
      }

      п += 1;
    }

    вернути РезультатПараметрів {
      параметри,
      помилка = пусто
    };
  }

  зовнішня дія перевірити_параметри_задекларованого_шаблону(компілятор: адреса<Компілятор>, контекст: Контекст, обʼєкт_задекларованого_шаблону: адреса<ОбʼєктШаблону>, дані_шаблону: розбирач::ДаніСполукиШаблон, місцезнаходження: розбирач::Місцезнаходження): Результат {
    якщо обʼєкт_задекларованого_шаблону.параметри.розмір != дані_шаблону.параметри.розмір {
      вернути Результат {
        помилка = створити_помилку(
          компілятор,
          "Невірна кількість параметрів шаблону.",
          створити_місцезнаходження_помилки_з_місцезнаходження_розбирача(компілятор, контекст.розібраний_текст, місцезнаходження)
        )
      };
    }

    // потім: перевірити назви

    вернути Результат { пусто };
  }

  структура КонтекстПереборуГотовихСтруктурШаблону {
    компілятор: адреса<Компілятор>;
    помилка: адреса<Помилка>;
    контекст: Контекст;
    обʼєкт_шаблону: адреса<ОбʼєктШаблону>;
  }

  дія дія_перебору_реєстру_готових_структури(реєстр: адреса<біб::Реєстр<ГотовіАргументиШаблону, Обʼєкт>>, аргумент: невідома_адреса, ключ: ГотовіАргументиШаблону, значення: Обʼєкт): логічне {
    змінна кпгсш = аргумент як адреса<КонтекстПереборуГотовихСтруктурШаблону>;
    змінна компілятор = кпгсш.компілятор;
    змінна контекст = кпгсш.контекст;
    змінна обʼєкт_шаблону = кпгсш.обʼєкт_шаблону;

    змінна обʼєкт_типу = значення.дані як адреса<ОбʼєктТипу>;
    змінна дані_обʼєкта_типу_структура = обʼєкт_типу.дані як ДаніОбʼєктаТипуСтруктура;

    змінна середовище_доповнення_структури = створити_середовище(компілятор, контекст.середовище);

    змінна п: позитивне = 0;

    поки п < обʼєкт_шаблону.параметри.розмір {
      змінна параметр = обʼєкт_шаблону.параметри.дані[п];
      змінна готовий_аргумент_шаблону = ключ.дані[п];

      якщо готовий_аргумент_шаблону.вид == ВидАргументаГотовихШаблонуОбʼєктТипу {
        змінити_в_середовищі(
          середовище_доповнення_структури,
          параметр.назва,
          Обʼєкт { ВидОбʼєктаТип, дані = готовий_аргумент_шаблону.значення як адреса<ОбʼєктТипу> }
        );
      } інакше якщо готовий_аргумент_шаблону.вид == ВидАргументаГотовихШаблонуОбʼєктСирогоЗначення {
        змінити_в_середовищі(
          середовище_доповнення_структури,
          параметр.назва,
          Обʼєкт { ВидОбʼєктаСиреЗначення, дані = готовий_аргумент_шаблону.значення як ОбʼєктСирогоЗначення }
        );
      }

      п += 1;
    }

    змінна контекст_виконання_шаблону = виділити<КонтекстКомпіляціїВиконанняШаблону>(компілятор);
    контекст_виконання_шаблону.обʼєкт_шаблону = обʼєкт_шаблону;
    контекст_виконання_шаблону.готові_аргументи_шаблону = ключ;

    змінна результат_доповнення_структури = скомпілювати_структуру(
      компілятор,
      Контекст {
        розібраний_текст = обʼєкт_шаблону.контекст_компіляції.розібраний_текст,
        середовище = середовище_доповнення_структури,
        кк_дії = обʼєкт_шаблону.контекст_компіляції.кк_дії
      },
      контекст_виконання_шаблону,
      обʼєкт_типу,
      обʼєкт_шаблону.сполука.дані як розбирач::ДаніСполукиСтруктура,
      обʼєкт_шаблону.сполука.місцезнаходження
    );

    якщо результат_доповнення_структури.помилка != пусто {
      кпгсш.помилка = результат_доповнення_структури.помилка;

      вернути ні;
    }

    вернути так;
  }

  дія дозаповнити_задекларований_шаблон_структури(компілятор: адреса<Компілятор>, контекст: Контекст, обʼєкт_задекларованого_шаблону: адреса<ОбʼєктШаблону>, дані_шаблону: розбирач::ДаніСполукиШаблон, місцезнаходження: розбирач::Місцезнаходження): Результат {
    змінна дані_структури = дані_шаблону.значення.дані як розбирач::ДаніСполукиСтруктура;
    змінна місцезнаходження_структури = дані_шаблону.значення.місцезнаходження;

    змінна дані_задекларованої_структури = обʼєкт_задекларованого_шаблону.сполука.дані як розбирач::ДаніСполукиСтруктура;
    змінна місцезнаходження_задекларованої_структури = обʼєкт_задекларованого_шаблону.сполука.місцезнаходження;

    якщо дані_структури.параметри.розмір == 0 {
      вернути Результат { пусто };
    } інакше {
      якщо дані_задекларованої_структури.параметри.розмір != 0 {
        якщо дані_шаблону.ідентифікатор.заповнено == позитивне(так) {
          змінна назва_т8 = розбирач::отримати_значення_слова(
            т8 { контекст.розібраний_текст.текст_коду.значення.розмір, контекст.розібраний_текст.текст_коду.значення.дані },
            контекст.розібраний_текст.слова.дані[дані_шаблону.ідентифікатор.значення.позиція_слова]
          );

          вернути Результат {
            помилка = створити_помилку_субʼєкт_вже_визначено(
              компілятор,
              Місцезнаходження { контекст.розібраний_текст, дані_шаблону.ідентифікатор.значення.позиція_слова },
              назва_т8
            )
          };
        } інакше {
          вернути Результат {
            помилка = створити_помилку_субʼєкт_вже_визначено(
              компілятор,
              Місцезнаходження { контекст.розібраний_текст, місцезнаходження.позиція_початкового_слова },
              т8 { 0, пусто }
            )
          };
        }
      }
    }

    обʼєкт_задекларованого_шаблону.сполука = дані_шаблону.значення;
    обʼєкт_задекларованого_шаблону.контекст_компіляції = контекст;

    змінна кпгсш = КонтекстПереборуГотовихСтруктурШаблону {
      компілятор,
      пусто,
      контекст,
      обʼєкт_задекларованого_шаблону
    };

    біб::перебрати_реєстр<ГотовіАргументиШаблону, Обʼєкт>(
      обʼєкт_задекларованого_шаблону.готові,
      дія_перебору_реєстру_готових_структури,
      кпгсш::адреса
    );

    якщо кпгсш.помилка != пусто {
      вернути Результат { кпгсш.помилка };
    }

    вернути Результат { пусто };
  }

  структура КонтекстПереборуГотовихДійШаблону {
    компілятор: адреса<Компілятор>;
    помилка: адреса<Помилка>;
    контекст: Контекст;
    обʼєкт_шаблону: адреса<ОбʼєктШаблону>;
  }

  дія дія_перебору_реєстру_готових_дії(реєстр: адреса<біб::Реєстр<ГотовіАргументиШаблону, Обʼєкт>>, аргумент: невідома_адреса, ключ: ГотовіАргументиШаблону, значення: Обʼєкт): логічне {
    змінна кпгдш = аргумент як адреса<КонтекстПереборуГотовихДійШаблону>;
    змінна компілятор = кпгдш.компілятор;
    змінна контекст = кпгдш.контекст;
    змінна обʼєкт_шаблону = кпгдш.обʼєкт_шаблону;

    змінна обʼєкт_дії = значення.дані як адреса<ОбʼєктДії>;

    змінна середовище_доповнення_дії = створити_середовище(компілятор, контекст.середовище);

    змінна п: позитивне = 0;

    поки п < обʼєкт_шаблону.параметри.розмір {
      змінна параметр = обʼєкт_шаблону.параметри.дані[п];
      змінна готовий_аргумент_шаблону = ключ.дані[п];

      якщо готовий_аргумент_шаблону.вид == ВидАргументаГотовихШаблонуОбʼєктТипу {
        змінити_в_середовищі(
          середовище_доповнення_дії,
          параметр.назва,
          Обʼєкт { ВидОбʼєктаТип, дані = готовий_аргумент_шаблону.значення як адреса<ОбʼєктТипу> }
        );
      } інакше якщо готовий_аргумент_шаблону.вид == ВидАргументаГотовихШаблонуОбʼєктСирогоЗначення {
        змінити_в_середовищі(
          середовище_доповнення_дії,
          параметр.назва,
          Обʼєкт { ВидОбʼєктаСиреЗначення, дані = готовий_аргумент_шаблону.значення як ОбʼєктСирогоЗначення }
        );
      }

      п += 1;
    }

    змінна контекст_виконання_шаблону = виділити<КонтекстКомпіляціїВиконанняШаблону>(компілятор);
    контекст_виконання_шаблону.обʼєкт_шаблону = обʼєкт_шаблону;
    контекст_виконання_шаблону.готові_аргументи_шаблону = ключ;

    змінна результат_доповнення_дії = скомпілювати_дію(
      компілятор,
      Контекст {
        розібраний_текст = обʼєкт_шаблону.контекст_компіляції.розібраний_текст,
        середовище = середовище_доповнення_дії,
        кк_дії = обʼєкт_шаблону.контекст_компіляції.кк_дії
      },
      контекст_виконання_шаблону,
      обʼєкт_дії,
      обʼєкт_шаблону.сполука.дані як розбирач::ДаніСполукиДія,
      обʼєкт_шаблону.сполука.місцезнаходження
    );

    якщо результат_доповнення_дії.помилка != пусто {
      кпгдш.помилка = результат_доповнення_дії.помилка;

      вернути ні;
    }

    вернути так;
  }

  дія дозаповнити_задекларований_шаблон_дії(компілятор: адреса<Компілятор>, контекст: Контекст, обʼєкт_задекларованого_шаблону: адреса<ОбʼєктШаблону>, дані_шаблону: розбирач::ДаніСполукиШаблон, місцезнаходження: розбирач::Місцезнаходження): Результат {
    змінна дані_дії = дані_шаблону.значення.дані як розбирач::ДаніСполукиДія;
    змінна місцезнаходження_дії = дані_шаблону.значення.місцезнаходження;

    змінна дані_задекларованої_дії = обʼєкт_задекларованого_шаблону.сполука.дані як розбирач::ДаніСполукиДія;
    змінна місцезнаходження_задекларованої_дії = обʼєкт_задекларованого_шаблону.сполука.місцезнаходження;

    обʼєкт_задекларованого_шаблону.сполука = дані_шаблону.значення;
    обʼєкт_задекларованого_шаблону.контекст_компіляції = контекст;

    змінна кпгдш = КонтекстПереборуГотовихДійШаблону {
      компілятор,
      пусто,
      контекст,
      обʼєкт_задекларованого_шаблону
    };

    біб::перебрати_реєстр<ГотовіАргументиШаблону, Обʼєкт>(
      обʼєкт_задекларованого_шаблону.готові,
      дія_перебору_реєстру_готових_дії,
      кпгдш::адреса
    );

    якщо кпгдш.помилка != пусто {
      вернути Результат { кпгдш.помилка };
    }

    вернути Результат { пусто };
  }

  зовнішня дія дозаповнити_задекларований_шаблон(компілятор: адреса<Компілятор>, контекст: Контекст, обʼєкт_задекларованого_шаблону: адреса<ОбʼєктШаблону>, дані_шаблону: розбирач::ДаніСполукиШаблон, місцезнаходження: розбирач::Місцезнаходження): Результат {
    змінна назва_т8 = т8 { 0, пусто };

    якщо дані_шаблону.ідентифікатор.заповнено == позитивне(так) {
      назва_т8 = розбирач::отримати_значення_слова(
        т8 { контекст.розібраний_текст.текст_коду.значення.розмір, контекст.розібраний_текст.текст_коду.значення.дані },
        контекст.розібраний_текст.слова.дані[дані_шаблону.ідентифікатор.значення.позиція_слова]
      );
    }

    змінна результат_перевірки_параметрів = перевірити_параметри_задекларованого_шаблону(
      компілятор,
      контекст,
      обʼєкт_задекларованого_шаблону,
      дані_шаблону,
      місцезнаходження
    );

    якщо результат_перевірки_параметрів.помилка != пусто {
      вернути результат_перевірки_параметрів;
    }

    якщо обʼєкт_задекларованого_шаблону.сполука.вид == розбирач::ВидСполукиСтруктура {
      якщо дані_шаблону.значення.вид == розбирач::ВидСполукиСтруктура {
        вернути дозаповнити_задекларований_шаблон_структури(
          компілятор,
          контекст,
          обʼєкт_задекларованого_шаблону,
          дані_шаблону,
          місцезнаходження
        );
      }
    } інакше якщо обʼєкт_задекларованого_шаблону.сполука.вид == розбирач::ВидСполукиДія {
      якщо дані_шаблону.значення.вид == розбирач::ВидСполукиДія {
        вернути дозаповнити_задекларований_шаблон_дії(
          компілятор,
          контекст,
          обʼєкт_задекларованого_шаблону,
          дані_шаблону,
          місцезнаходження
        );
      }
    }

    вернути Результат {
      помилка = створити_помилку_субʼєкт_вже_визначено(
        компілятор,
        Місцезнаходження { контекст.розібраний_текст, дані_шаблону.ідентифікатор.значення.позиція_слова },
        назва_т8
      )
    };
  }

  зовнішня дія скомпілювати_шаблон(компілятор: адреса<Компілятор>, контекст: Контекст, дані_шаблону: розбирач::ДаніСполукиШаблон, місцезнаходження: розбирач::Місцезнаходження): РезультатОбʼєктаШаблону {
    змінна назва_т8 = т8 { 0, пусто };

    якщо дані_шаблону.ідентифікатор.заповнено == позитивне(так) {
      назва_т8 = розбирач::отримати_значення_слова(
        т8 { контекст.розібраний_текст.текст_коду.значення.розмір, контекст.розібраний_текст.текст_коду.значення.дані },
        контекст.розібраний_текст.слова.дані[дані_шаблону.ідентифікатор.значення.позиція_слова]
      );
    }

    змінна результат_параметрів = скомпілювати_параметри_шаблону(компілятор, контекст, дані_шаблону.параметри, місцезнаходження);
    якщо результат_параметрів.помилка != пусто {
      вернути РезультатОбʼєктаШаблону {
        ...,
        помилка = результат_параметрів.помилка
      };
    }

    змінна обʼєкт_шаблону = виділити_обʼєкт_шаблону(компілятор);

    обʼєкт_шаблону.назва = назва_зовнішня(назва_т8);
    обʼєкт_шаблону.параметри = результат_параметрів.параметри;
    обʼєкт_шаблону.сполука = дані_шаблону.значення;
    обʼєкт_шаблону.контекст_компіляції = контекст;

    вернути РезультатОбʼєктаШаблону {
      обʼєкт_шаблону,
      помилка = пусто
    };
  }

  зовнішня дія скомпілювати_визначити_шаблон(компілятор: адреса<Компілятор>, контекст: Контекст, дані_шаблону: розбирач::ДаніСполукиШаблон, місцезнаходження: розбирач::Місцезнаходження): РезультатОбʼєктаШаблону {
    змінна назва_т8 = розбирач::отримати_значення_слова(
      т8 { контекст.розібраний_текст.текст_коду.значення.розмір, контекст.розібраний_текст.текст_коду.значення.дані },
      контекст.розібраний_текст.слова.дані[дані_шаблону.ідентифікатор.значення.позиція_слова]
    );

    змінна обʼєкт_з_середовища: Обʼєкт;
    якщо знайти_в_середовищі(контекст.середовище, назва_зовнішня(назва_т8), обʼєкт_з_середовища::адреса) {
      якщо обʼєкт_з_середовища.вид == ВидОбʼєктаШаблон {
        змінна обʼєкт_шаблону = обʼєкт_з_середовища.дані як адреса<ОбʼєктШаблону>;
        
        змінна результат_дозаповнення = дозаповнити_задекларований_шаблон(
          компілятор,
          контекст,
          обʼєкт_шаблону,
          дані_шаблону,
          місцезнаходження
        );
        
        якщо результат_дозаповнення.помилка != пусто {
          вернути РезультатОбʼєктаШаблону { ..., помилка = результат_дозаповнення.помилка };
        }

        вернути РезультатОбʼєктаШаблону { обʼєкт_шаблону, помилка = пусто };
      }

      вернути РезультатОбʼєктаШаблону {
        ...,
        помилка = створити_помилку_субʼєкт_вже_визначено(
          компілятор,
          Місцезнаходження { контекст.розібраний_текст, дані_шаблону.ідентифікатор.значення.позиція_слова },
          назва_т8
        )
      };
    }

    змінна результат_шаблону = скомпілювати_шаблон(
      компілятор,
      контекст,
      дані_шаблону,
      місцезнаходження
    );

    якщо результат_шаблону.помилка != пусто {
      вернути РезультатОбʼєктаШаблону { ..., помилка = результат_шаблону.помилка };
    }

    змінити_в_середовищі(контекст.середовище, назва_зовнішня(назва_т8), Обʼєкт { ВидОбʼєктаШаблон, результат_шаблону.обʼєкт_шаблону });

    вернути РезультатОбʼєктаШаблону {
      результат_шаблону.обʼєкт_шаблону,
      помилка = пусто
    };
  }
}
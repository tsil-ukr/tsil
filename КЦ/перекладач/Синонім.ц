взяти визначення КЦ;

секція КЦ {
  місцева дія скомпілювати_синонім(компілятор: адреса<Компілятор>, контекст: Контекст, контекст_виконання_шаблону: адреса<КонтекстКомпіляціїВиконанняШаблону>, дані_синоніма: розбирач::ДаніСполукиСинонім, місцезнаходження: розбирач::Місцезнаходження) -> РезультатОбʼєкта {
    вернути скомпілювати_значення(
      компілятор,
      контекст,
      пусто,
      дані_синоніма.значення,
      пусто
    );
  }

  місцева дія скомпілювати_визначити_синонім(компілятор: адреса<Компілятор>, контекст: Контекст, дані_синоніма: розбирач::ДаніСполукиСинонім, місцезнаходження: розбирач::Місцезнаходження) -> РезультатОбʼєкта {
    змінна назва_т8 = т8 { 0, пусто };
    
    якщо не розбирач::отримати_значення_слова(
      ю8 { контекст.розібраний_текст.текст_коду.значення.розмір, контекст.розібраний_текст.текст_коду.значення.дані },
      контекст.розібраний_текст.слова.дані[дані_синоніма.ідентифікатор.позиція_слова],
      назва_т8::адреса
    ) {
      вернути РезультатОбʼєкта {
        помилка = створити_помилку(
          компілятор,
          "Не вдалось отримати назву змінної.",
          створити_місцезнаходження_помилки_з_місцезнаходження_розбирача(компілятор, контекст.розібраний_текст, місцезнаходження)
        ),
        ...
      };
    }

    змінна назва = назва_без_копіювання(компілятор, назва_т8);

    змінна обʼєкт_з_середовища: Обʼєкт;
    якщо знайти_в_середовищі(контекст.середовище, назва, обʼєкт_з_середовища::адреса) {
      вернути РезультатОбʼєкта {
        ...,
        помилка = створити_помилку(
          компілятор,
          "Субʼєкт вже визначено.",
          створити_місцезнаходження_помилки_з_позиції_слова(компілятор, контекст.розібраний_текст, дані_синоніма.ідентифікатор.позиція_слова)
        )
      };
    }

    змінна результат = скомпілювати_синонім(
      компілятор,
      контекст,
      пусто,
      дані_синоніма,
      місцезнаходження
    );

    якщо результат.помилка != пусто {
      вернути результат;
    }

    змінити_в_середовищі(контекст.середовище, назва, результат.обʼєкт);

    вернути результат;
  }
}
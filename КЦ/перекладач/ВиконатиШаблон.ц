взяти визначення КЦ;

секція КЦ {
  місцева дія скомпілювати_виконати_шаблон(компілятор: адреса<Компілятор>, контекст: Контекст, дані_виконати_шаблон: розбирач::ДаніСполукиВиконатиШаблон, місцезнаходження: розбирач::Місцезнаходження, очікуваний_тип: адреса<ОбʼєктТипу>): РезультатОбʼєкта {
    змінна результат_ліво = скомпілювати_значення(компілятор, контекст, пусто, дані_виконати_шаблон.ліво, пусто);
    якщо результат_ліво.помилка != пусто {
      вернути РезультатОбʼєкта { помилка = результат_ліво.помилка, ... };
    }

    змінна аргументи = виділити_памʼять<Аргумент>(
      компілятор,
      дані_виконати_шаблон.аргументи.розмір
    );

    змінна п: позитивне = 0;
    поки п < дані_виконати_шаблон.аргументи.розмір {
      змінна результат_аргумента = скомпілювати_значення(
        компілятор,
        контекст,
        пусто,
        дані_виконати_шаблон.аргументи.дані[п].значення,
        пусто
      );

      якщо результат_аргумента.помилка != пусто {
        звільнити_памʼять(компілятор, аргументи);

        вернути РезультатОбʼєкта { помилка = результат_аргумента.помилка, ... };
      }

      аргументи[п] = Аргумент {
        обʼєкт = результат_аргумента.обʼєкт,
        місцезнаходження = дані_виконати_шаблон.аргументи.дані[п].значення.місцезнаходження
      };

      п += 1;
    }

    змінна результат = обʼєкт_виконати_шаблон(
      компілятор,
      контекст,
      місцезнаходження,
      результат_ліво.обʼєкт,
      дані_виконати_шаблон.аргументи.розмір,
      аргументи
    );

    звільнити_памʼять(компілятор, аргументи);

    вернути результат;
  }
}
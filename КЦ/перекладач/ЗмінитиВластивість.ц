взяти визначення КЦ;

секція КЦ {
  місцева дія скомпілювати_змінити_властивість(компілятор: адреса<Компілятор>, контекст: Контекст, дані_змінити_властивість: розбирач::ДаніСполукиЗмінитиВластивість, місцезнаходження: розбирач::Місцезнаходження) -> РезультатОбʼєкта {
    змінна результат_ліво = скомпілювати_значення(компілятор, контекст, пусто, дані_змінити_властивість.ліво, пусто);
    якщо результат_ліво.помилка != пусто {
      вернути РезультатОбʼєкта { помилка = результат_ліво.помилка, ... };
    }

    змінна результат_значення = скомпілювати_значення(компілятор, контекст, пусто, дані_змінити_властивість.значення, пусто);
    якщо результат_значення.помилка != пусто {
      вернути РезультатОбʼєкта { помилка = результат_значення.помилка, ... };
    }

    змінна назва_т8 = т8 { 0, пусто };
    
    якщо не розбирач::отримати_значення_ідентифікатора(
      ю8 { контекст.розібраний_текст.текст_коду.значення.розмір, контекст.розібраний_текст.текст_коду.значення.дані },
      контекст.розібраний_текст.слова,
      дані_змінити_властивість.ідентифікатор,
      назва_т8::адреса
    ) {
      вернути РезультатОбʼєкта {
        помилка = створити_помилку(
          компілятор,
          "Не вдалось отримати назву властивості.",
          створити_місцезнаходження_помилки_з_місцезнаходження_розбирача(компілятор, контекст.розібраний_текст, місцезнаходження)
        ),
        ...
      };
    }

    змінна назва = назва_без_копіювання(компілятор, назва_т8);

    змінна результат = обʼєкт_змінити_властивість(
      компілятор,
      контекст,
      місцезнаходження,
      результат_ліво.обʼєкт,
      назва,
      Аргумент { результат_значення.обʼєкт, дані_змінити_властивість.значення.місцезнаходження }
    );

    вернути результат;
  }
}
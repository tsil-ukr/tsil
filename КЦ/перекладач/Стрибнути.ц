взяти визначення КЦ;

секція КЦ {
  місцева дія скомпілювати_стрибнути(компілятор: адреса<Компілятор>, контекст: Контекст, дані_стрибнути: розбирач::ДаніСполукиСтрибнути, місцезнаходження: розбирач::Місцезнаходження): Результат {
    змінна назва_т8 = т8 { 0, пусто };
    
    якщо не розбирач::отримати_значення_слова(
      ю8 { контекст.розібраний_текст.текст_коду.значення.розмір, контекст.розібраний_текст.текст_коду.значення.дані },
      контекст.розібраний_текст.слова.дані[дані_стрибнути.ідентифікатор.позиція_слова],
      назва_т8::адреса
    ) {
      вернути Результат {
        помилка = створити_помилку(
          компілятор,
          "Не вдалось отримати назву кроку для стрибку.",
          створити_місцезнаходження_помилки_з_місцезнаходження_розбирача(компілятор, контекст.розібраний_текст, місцезнаходження)
        )
      };
    }

    змінна назва = назва_без_копіювання(компілятор, назва_т8);

    змінна пм_стрибок = ПМ::вказівка_стрибнути(контекст.кк_дії.поточний_крок.пм_крок, контекст.кк_дії.поточний_крок.пм_крок);

    покласти_стрибок(
      компілятор,
      контекст.кк_дії.обʼєкт_дії.стрибки::адреса,
      Стрибок { назва, контекст.кк_дії.обʼєкт_дії, пм_стрибок, місцезнаходження }
    );

    вернути Результат {
      помилка = пусто
    };
  }
}
взяти визначення КЦ;

секція КЦ {
  зовнішня дія скомпілювати_вернути(компілятор: адреса<Компілятор>, контекст: Контекст, дані_вернути: розбирач::ДаніСполукиВернути, місцезнаходження: розбирач::Місцезнаходження): Результат {
    якщо контекст.кк_дії.значення_вернути.тип == компілятор.тип_ніщо {
      якщо дані_вернути.значення != пусто {
        змінна результат_значення = скомпілювати_значення_як_значення(компілятор, контекст, дані_вернути.значення, контекст.кк_дії.значення_вернути.тип);
        якщо результат_значення.помилка != пусто {
          вернути Результат { помилка = результат_значення.помилка };
        }

        вернути Результат {
          помилка = створити_помилку_невірний_тип_вернути(
            компілятор,
            контекст.розібраний_текст,
            місцезнаходження,
            контекст.кк_дії.значення_вернути.тип,
            результат_значення.обʼєкт_значення.тип
          )
        };
      }

      ПМ::вказівка_стрибнути(контекст.кк_дії.поточний_крок.пм_крок, контекст.кк_дії.крок_вернути.пм_крок);

      вернути Результат { помилка = пусто };
    } інакше {
      якщо дані_вернути.значення == пусто {
        вернути Результат {
          помилка = створити_помилку_невірний_тип_вернути(
            компілятор,
            контекст.розібраний_текст,
            місцезнаходження,
            контекст.кк_дії.значення_вернути.тип,
            компілятор.тип_ніщо
          )
        };
      }

      змінна результат_значення = скомпілювати_значення_як_значення(компілятор, контекст, дані_вернути.значення, контекст.кк_дії.значення_вернути.тип);
      якщо результат_значення.помилка != пусто {
        вернути Результат { помилка = результат_значення.помилка };
      }

      якщо перевірити_тип_на_запис_і_конвертувати_якщо_треба(компілятор, контекст.кк_дії.значення_вернути.тип, результат_значення.обʼєкт_значення::адреса) == ні {
        вернути Результат {
          помилка = створити_помилку_невірний_тип_вернути(
            компілятор,
            контекст.розібраний_текст,
            місцезнаходження,
            контекст.кк_дії.значення_вернути.тип,
            результат_значення.обʼєкт_значення.тип
          )
        };
      }

      ПМ::вказівка_записати(
        контекст.кк_дії.поточний_крок.пм_крок,
        результат_значення.обʼєкт_значення.пм_значення,
        контекст.кк_дії.значення_вернути.пм_значення
      );

      ПМ::вказівка_стрибнути(контекст.кк_дії.поточний_крок.пм_крок, контекст.кк_дії.крок_вернути.пм_крок);

      вернути Результат { помилка = пусто };
    }
  }
}
взяти визначення КЦ;

секція КЦ {
  зовнішня дія скомпілювати_отримати_з_середовища(компілятор: адреса<Компілятор>, контекст: Контекст, дані_прочитати: розбирач::ДаніСполукиПрочитати, місцезнаходження: розбирач::Місцезнаходження, очікуваний_тип: адреса<ОбʼєктТипу>): РезультатОбʼєкта {
    змінна назва_ю8 = розбирач::отримати_значення_ідентифікатора(
      ю8 { контекст.розібраний_текст.текст_коду.значення.розмір, контекст.розібраний_текст.текст_коду.значення.дані },
      контекст.розібраний_текст.слова,
      дані_прочитати.ідентифікатор
    );
    змінна назва_т8 = отримати_т8_з_ю8(компілятор, назва_ю8);

    змінна обʼєкт_з_середовища: Обʼєкт;
    якщо знайти_в_середовищі_рекурсивно(контекст.середовище, назва_зовнішня(назва_т8), обʼєкт_з_середовища::адреса) {
      звільнити_памʼять(компілятор, назва_т8.дані);

      вернути РезультатОбʼєкта { обʼєкт_з_середовища, пусто };
    }

    звільнити_памʼять(компілятор, назва_т8.дані);

    вернути РезультатОбʼєкта {
      ...,
      помилка = створити_помилку(
        компілятор,
        "Субʼєкт не визначено.",
        створити_місцезнаходження_помилки_з_місцезнаходження(компілятор, контекст.розібраний_текст, місцезнаходження)
      )
    };
  }
}
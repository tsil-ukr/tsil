взяти визначення Ц;

секція цк {
  місцева дія створити_середовище(батьківське: комірка<Середовище>): комірка<Середовище> {
    ціль с = виділити<Середовище>();
    с.батьківське = батьківське;
    с.субʼєкти = створити_карту_субʼєктів();
    с._секція = пусто;
    вернути с;
  }

  місцева дія отримати_з_середовища(середовище: комірка<Середовище>, назва: памʼять<п8>): РезультатОтриманняЗСередовища {
    змінна останнє_середовище = середовище;
    поки останнє_середовище != пусто {
      ціль вид: позитивне = 0;
      ціль дані: невідома_комірка = пусто;
      ціль знайдено = отримати_з_карти_субʼєктів(останнє_середовище.субʼєкти, назва, комірка(вид), комірка(дані));
      якщо знайдено == так {
        вернути РезультатОтриманняЗСередовища { так, ОбʼєктКомпіляції { вид, дані як комірка<Тип> } };
      }
      останнє_середовище = останнє_середовище.батьківське;
    }
    вернути РезультатОтриманняЗСередовища { ні };
  }

  місцева дія отримати_з_локального_середовища(середовище: комірка<Середовище>, назва: памʼять<п8>): РезультатОтриманняЗСередовища {
    ціль вид: позитивне = 0;
    ціль дані: невідома_комірка = пусто;
    ціль знайдено = отримати_з_карти_субʼєктів(середовище.субʼєкти, назва, комірка(вид), комірка(дані));
    якщо знайдено == так {
      вернути РезультатОтриманняЗСередовища { так, ОбʼєктКомпіляції { вид, дані як комірка<Тип> } };
    }
    вернути РезультатОтриманняЗСередовища { ні };
  }

  місцева дія змінити_в_середовищі(середовище: комірка<Середовище>, назва: памʼять<п8>, значення: ОбʼєктКомпіляції) {
    змінити_в_карті_субʼєктів(середовище.субʼєкти, назва, значення.вид, (значення.дані як комірка<Тип>) як невідома_комірка);
  }

  місцева дія отримати_префікс_назви(середовище: комірка<Середовище>): памʼять<п8> {
    ціль префікс: памʼять<п8> = пусто;
    змінна останнє_середовище = середовище;
    поки останнє_середовище != пусто {
      якщо останнє_середовище._секція != пусто {
        якщо останнє_середовище._секція.назва != пусто {
          якщо префікс == пусто {
            префікс = останнє_середовище._секція.назва;
          } інакше {
            префікс = зʼєднати_ю8(зʼєднати_ю8(останнє_середовище._секція.назва, ю8"::"), префікс);
          }
        }
      }
      останнє_середовище = останнє_середовище.батьківське;
    }
    вернути префікс;
  }
}
взяти визначення Ц;

секція ц {
  місцева дія порівняти_ю8(а: памʼять<п8>, б: памʼять<п8>): логічне {
    змінна символ_: п8 = а[0];
    змінна х: позитивне = 0;
    поки символ_ != 0 {
      якщо символ_ != б[х] {
        вернути ні;
      }
      х = х + 1;
      символ_ = а[х];
    }
    якщо б[х] != 0 {
      вернути ні;
    }
    вернути так;
  }

  місцева дія створити_середовище(вище: комірка<Середовище>): комірка<Середовище> {
    ціль середовище = виділити<Середовище>();
    середовище.вище = вище;
    середовище.субʼєкти = виділити_карту<памʼять<п8>, Обʼєкт>(порівняти_ю8);
    вернути середовище;
  }

  місцева дія отримати_зі_середовища(середовище: комірка<Середовище>, назва: памʼять<п8>, куда_записати: комірка<Обʼєкт>): логічне {
    ціль останнє_середовище = середовище;
    поки останнє_середовище != пусто {
      ціль обʼєкт = отримати_значення_з_карти<памʼять<п8>, Обʼєкт>(останнє_середовище.субʼєкти, назва, Обʼєкт { 0 });
      якщо обʼєкт.вид != 0 {
        куда_записати.вид = обʼєкт.вид;
        якщо обʼєкт.вид == ВидОбʼєктаЗначення {
          куда_записати.дані = обʼєкт.дані як ОбʼєктЗначення;
        } інакше {
          куда_записати.дані = обʼєкт.дані як комірка<ОбʼєктТипу>;
        }
        вернути так;
      }
      останнє_середовище = останнє_середовище.вище;
    }
    вернути ні;
  }

  місцева дія отримати_з_локального_середовища(середовище: комірка<Середовище>, назва: памʼять<п8>, куда_записати: комірка<Обʼєкт>): логічне {
    ціль обʼєкт = отримати_значення_з_карти<памʼять<п8>, Обʼєкт>(середовище.субʼєкти, назва, Обʼєкт { 0 });
    якщо обʼєкт.вид != 0 {
      куда_записати.вид = обʼєкт.вид;
        якщо обʼєкт.вид == ВидОбʼєктаЗначення {
          куда_записати.дані = обʼєкт.дані як ОбʼєктЗначення;
        } інакше {
          куда_записати.дані = обʼєкт.дані як комірка<ОбʼєктТипу>;
        }
      вернути так;
    }
    вернути ні;
  }

  місцева дія змінити_в_середовищі(середовище: комірка<Середовище>, назва: памʼять<п8>, обʼєкт: Обʼєкт) {
    змінити_значення_карти<памʼять<п8>, Обʼєкт>(середовище.субʼєкти, назва, обʼєкт);
  }

//  місцева дія отримати_префікс_назви(середовище: комірка<Середовище>): памʼять<п8> {
//    ціль префікс: памʼять<п8> = пусто;
//    змінна останнє_середовище = середовище;
//    поки останнє_середовище != пусто {
//      якщо останнє_середовище.секція_ != пусто {
//        якщо останнє_середовище.секція_.назва != пусто {
//          якщо префікс == пусто {
//            префікс = останнє_середовище.секція_.назва;
//          } інакше {
//            префікс = зʼєднати_ю8(зʼєднати_ю8(останнє_середовище.секція_.назва, ю8"::"), префікс);
//          }
//        }
//      }
//      останнє_середовище = останнє_середовище.батьківське;
//    }
//    вернути префікс;
//  }
}
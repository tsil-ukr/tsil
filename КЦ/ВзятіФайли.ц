взяти визначення ./КЦ;

секція КЦ {
  дія перед_заміною_в_реєстрі(реєстр: адреса<біб::Реєстр<Шлях, адреса<РозібранийТекст>>>, ак: Шлях, аз: адреса<РозібранийТекст>, бк: адреса<Шлях>, бз: адреса<адреса<РозібранийТекст>>) {
    змінна компілятор = реєстр.аргумент як адреса<Компілятор>;

    знищити_шлях(компілятор, ак);
  }

  дія перед_видаленням_з_реєстру(реєстр: адреса<біб::Реєстр<Шлях, адреса<РозібранийТекст>>>, ключ: Шлях, значення: адреса<РозібранийТекст>) {
    змінна компілятор = реєстр.аргумент як адреса<Компілятор>;

    знищити_шлях(компілятор, ключ);
  }

  місцева дія створити_взяті_файли(компілятор: адреса<Компілятор>) -> адреса<ВзятіФайли> {
    змінна взяті_файли = виділити<ВзятіФайли>(компілятор);

    взяті_файли.компілятор = компілятор;
    взяті_файли.реєстр = біб::виділити_реєстр<Шлях, адреса<РозібранийТекст>>(
      компілятор.виділяч,
      0,
      компілятор,
      змішати_шлях_для_реєстру<адреса<РозібранийТекст>>,
      перевірити_шляхи_в_реєстрі<адреса<РозібранийТекст>>,
      перед_заміною_в_реєстрі,
      перед_видаленням_з_реєстру
    );

    вернути взяті_файли;
  }

  місцева дія знищити_взяті_файли(взяті_файли: адреса<ВзятіФайли>) {
    змінна компілятор = взяті_файли.компілятор;

    біб::звільнити_реєстр<Шлях, адреса<РозібранийТекст>>(взяті_файли.реєстр);

    звільнити(компілятор, взяті_файли);
  }

  місцева дія покласти_взятий_файл(взяті_файли: адреса<ВзятіФайли>, шлях: Шлях, розібраний_текст: адреса<РозібранийТекст>) {
    змінна компілятор = взяті_файли.компілятор;

    шлях = клонувати_шлях(компілятор, шлях);

    біб::змінити_в_реєстрі<Шлях, адреса<РозібранийТекст>>(
      взяті_файли.реєстр,
      шлях,
      розібраний_текст
    );
  }

  місцева дія знайти_взятий_файл(взяті_файли: адреса<ВзятіФайли>, шлях: Шлях, вихід: адреса<адреса<РозібранийТекст>>) -> логічне {
    вернути біб::знайти_в_реєстрі<Шлях, адреса<РозібранийТекст>>(
      взяті_файли.реєстр,
      шлях,
      вихід
    );
  }
}
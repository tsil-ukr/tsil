взяти визначення ./біб;

структура Накопичувач<Т>;

структура Накопичувач<Т> {
  розмір: позитивне;
  дані: памʼять<Т>;
}

дія зробити_накопичувач<Т>(): Накопичувач<Т> {
  ціль накопичувач: Накопичувач<Т>;
  накопичувач.розмір = 0;
  накопичувач.дані = пусто;
  вернути накопичувач;
}

дія виділити_накопичувач<Т>(): адреса<Накопичувач<Т>> {
  ціль накопичувач = виділити<Накопичувач<Т>>();
  накопичувач.розмір = 0;
  накопичувач.дані = пусто;
  вернути накопичувач;
}

дія покласти_в_накопичувач<Т>(накопичувач: адреса<Накопичувач<Т>>, дані: Т) {
  накопичувач.дані = перевиділити<Т>(накопичувач.дані, накопичувач.розмір + 1);
  накопичувач.дані[накопичувач.розмір] = дані;
  накопичувач.розмір = накопичувач.розмір + 1;
}

дія забрати_з_накопичувача<Т>(накопичувач: адреса<Накопичувач<Т>>): Т {
  ціль дані = накопичувач.дані[накопичувач.розмір - 1];
  накопичувач.розмір = накопичувач.розмір - 1;
  вернути дані;
}

дія звільнити_значення_накопичувача<Т>(накопичувач: Накопичувач<Т>) {
  звільнити(накопичувач.дані як адреса);
}

дія звільнити_накопичувач<Т>(накопичувач: адреса<Накопичувач<Т>>) {
  звільнити(накопичувач.дані як адреса);
  звільнити(накопичувач як адреса);
}

дія обернути_накопичувач<Т>(накопичувач: адреса<Накопичувач<Т>>) {
  якщо накопичувач.розмір == 0 {
    вернути;
  }
  змінна ціль па: позитивне = 0;
  змінна ціль пб: позитивне = накопичувач.розмір - 1;
  поки па < пб {
    змінна ціль тимчасове = накопичувач.дані[па];
    накопичувач.дані[па] = накопичувач.дані[пб];
    накопичувач.дані[пб] = тимчасове;
    па += 1;
    пб -= 1;
  }
}

дія зробити_копію_накопичувача<Т>(накопичувач: Накопичувач<Т>): Накопичувач<Т> {
  ціль копія = зробити_накопичувач<Т>();
  копія.розмір = накопичувач.розмір;
  копія.дані = виділити<Т>(ц32(копія.розмір));
  змінна ціль п: позитивне = 0;
  поки п < копія.розмір {
    копія.дані[п] = накопичувач.дані[п];
    п += 1;
  }
  вернути копія;
}

дія розширити_накопичувач<Т>(накопичувач: адреса<Накопичувач<Т>>, розмір: позитивне, пусте_значення: Т) {
  якщо розмір <= накопичувач.розмір {
    вернути;
  }
  накопичувач.дані = перевиділити<Т>(накопичувач.дані, ц32(розмір));
  змінна ціль п: позитивне = накопичувач.розмір;
  поки п < розмір {
    накопичувач.дані[п] = пусте_значення;
    п += 1;
  }
  накопичувач.розмір = розмір;
}

дія видалити_з_накопичувача<Т>(накопичувач: адреса<Накопичувач<Т>>, позиція: позитивне): логічне {
  якщо позиція >= накопичувач.розмір {
    вернути ні;
  }
  змінна ціль п: позитивне = позиція;
  поки п < накопичувач.розмір - 1 {
    накопичувач.дані[п] = накопичувач.дані[п + 1];
    п += 1;
  }
  накопичувач.розмір = накопичувач.розмір - 1;
  вернути так;
}

синонім НакопичувачЮ8 = Накопичувач<п8>;

дія зробити_накопичувач_ю8(): НакопичувачЮ8 {
  вернути зробити_накопичувач<п8>();
}

дія покласти_в_накопичувач_ю8(буфер: адреса<НакопичувачЮ8>, значення: памʼять<п8>) {
  змінна ціль х: позитивне = 0;
  поки значення[х] != 0 {
    покласти_в_накопичувач<п8>(буфер, значення[х]);
    х = х + 1;
  }
}

дія видалити_останнє_значення_з_накопичувача_ю8(буфер: адреса<НакопичувачЮ8>) {
  буфер.розмір = буфер.розмір - 1;
}

дія покласти_символ_в_накопичувач_ю8(буфер: адреса<НакопичувачЮ8>, значення: п8) {
  покласти_в_накопичувач<п8>(буфер, значення);
}

дія звільнити_значення_накопичувача_ю8(буфер: НакопичувачЮ8) {
  звільнити_значення_накопичувача<п8>(буфер);
}

дія виділити_ю8_з_накопичувача_ю8(буфер: адреса<НакопичувачЮ8>): памʼять<п8> {
  ціль значення = виділити<п8>(буфер.розмір + 1);
  змінна ціль х: позитивне = 0;
  поки х < буфер.розмір {
    значення[х] = буфер.дані[х];
    х = х + 1;
  }
  значення[буфер.розмір] = 0;
  вернути значення;
}

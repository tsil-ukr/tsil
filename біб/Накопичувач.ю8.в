взяти визначення ./Виділяч;

секція біб {
  структура Накопичувач<Т> {
    виділяч: адреса<Виділяч>;
    розмір: позитивне;
    дані: памʼять<Т>;
    вмісткість: позитивне;
  }

  дія виділити_накопичувач<Т>(виділяч: адреса<Виділяч>): адреса<Накопичувач<Т>> {
    змінна накопичувач = виділити<Накопичувач<Т>>(виділяч);

    накопичувач.виділяч = виділяч;
    накопичувач.розмір = 0;
    накопичувач.дані = пусто;
    накопичувач.вмісткість = 0;

    вернути накопичувач;
  }

  дія зробити_накопичувач<Т>(виділяч: адреса<Виділяч>): Накопичувач<Т> {
    змінна накопичувач: Накопичувач<Т>;

    накопичувач.виділяч = виділяч;
    накопичувач.розмір = 0;
    накопичувач.дані = пусто;
    накопичувач.вмісткість = 0;

    вернути накопичувач;
  }

  дія покласти_в_накопичувач<Т>(накопичувач: адреса<Накопичувач<Т>>, значення: Т) {
    якщо накопичувач.розмір == накопичувач.вмісткість {
      накопичувач.вмісткість += 1;
      накопичувач.дані = перевиділити_памʼять<Т>(накопичувач.виділяч, накопичувач.дані, накопичувач.вмісткість);
    }

    накопичувач.дані[накопичувач.розмір] = значення;
    накопичувач.розмір += 1;
  }

  дія звільнити_значення_накопичувача<Т>(накопичувач: Накопичувач<Т>) {
    біб::звільнити_памʼять(накопичувач.виділяч, накопичувач.дані);
  }

  дія звільнити_накопичувач<Т>(накопичувач: адреса<Накопичувач<Т>>) {
    звільнити_значення_накопичувача<Т>(накопичувач::вміст);
    біб::звільнити(накопичувач.виділяч, накопичувач);
  }
}
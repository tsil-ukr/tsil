взяти визначення біб/цілеспрямовано;
взяти визначення біб/Виділяч;
взяти визначення біб/Друк;

дія вивести_опції(опції: цілеспрямовано::РозібраніОпції<ю8>) {
  змінна п: позитивне = 0;

  поки п < опції.розмір {
    біб::вивести("--");
    біб::вивести_ю8(опції.дані[п].ключ);
    якщо опції.дані[п].значення.заповнено == позитивне(так) {
      біб::вивести("=\"");
      біб::вивести_ю8(опції.дані[п].значення.значення);
      біб::вивести("\"");
    }

    якщо п < опції.розмір - 1 {
      біб::вивести(" ");
    }

    п += 1;
  }
}

зовнішня дія розпочати(кількість_аргументів: позитивне, аргументи: памʼять<ю8>): ц32 {
  змінна виділяч = біб::отримати_системний_виділяч();

  змінна доступні_команди = цілеспрямовано::ДоступніКомандиДляРозбору<ю8> { 1, біб::виділити_памʼять<ю8>(виділяч, 1) };
  доступні_команди.дані[0] = ю8"скомпілювати";

  змінна розібрана_команда = цілеспрямовано::зробити_розібрану_команду<ю8>();

  змінна успіх = цілеспрямовано::розібрати_команду<ю8>(
    виділяч,
    цілеспрямовано::АргументиДляРозбору<ю8> { кількість_аргументів - 1, аргументи[1]::адреса як памʼять<ю8> },
    доступні_команди,
    ю8"--",
    ю8"=",
    розібрана_команда::адреса
  );

  якщо успіх {
    змінна п: позитивне = 0;

    біб::вивести("опції програми: ");
    вивести_опції(розібрана_команда.опції_програми);
    біб::надрукувати("");

    біб::надрукувати("виходи:");
    поки п < розібрана_команда.виходи.розмір {
      біб::вивести("  ");
      біб::надрукувати_ю8(розібрана_команда.виходи.дані[п].значення);
      біб::вивести("    опції: ");
      вивести_опції(розібрана_команда.виходи.дані[п].опції);
      біб::надрукувати("");
      п += 1;
    }

    біб::вивести("команда: ");
    біб::надрукувати_ю8(розібрана_команда.команда);
    біб::вивести("опції команди: ");
    вивести_опції(розібрана_команда.опції_команди);
    біб::надрукувати("");

    п = 0;

    біб::надрукувати("входи:");
    поки п < розібрана_команда.входи.розмір {
      біб::вивести("  ");
      біб::надрукувати_ю8(розібрана_команда.входи.дані[п].значення);
      біб::вивести("    опції: ");
      вивести_опції(розібрана_команда.входи.дані[п].опції);
      біб::надрукувати("");
      п += 1;
    }
  } інакше {
    біб::надрукувати("не вдалось розібрати");
  }

  цілеспрямовано::знищити_розібрану_команду<ю8>(виділяч, розібрана_команда);
  біб::звільнити_памʼять(виділяч, доступні_команди.дані);

  вернути 0;
}